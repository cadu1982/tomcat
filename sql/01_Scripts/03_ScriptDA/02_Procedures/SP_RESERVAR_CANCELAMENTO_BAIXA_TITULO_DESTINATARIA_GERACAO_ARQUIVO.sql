USE AB_DDA
GO

IF NOT EXISTS (SELECT * FROM sys.objects WHERE name = 'SP_RESERVAR_CANCELAMENTO_BAIXA_TITULO_DESTINATARIA_GERACAO_ARQUIVO')
BEGIN
	EXEC ('CREATE PROCEDURE dbo.SP_RESERVAR_CANCELAMENTO_BAIXA_TITULO_DESTINATARIA_GERACAO_ARQUIVO AS BEGIN RETURN END')
END
GO

ALTER PROCEDURE dbo.SP_RESERVAR_CANCELAMENTO_BAIXA_TITULO_DESTINATARIA_GERACAO_ARQUIVO (  
	@P_NOMEARQREM VARCHAR(255),  
	@P_QTD INT  
) AS   
BEGIN  
	/*
		26/01/2023 - RFAQUINI - CRIAÇÃO (COM BASE NA SP_RESERVAR_CANCELAMENTO_BAIXA_TITULO_GERACAO_ARQUIVO - ADDA115)
	*/
  
	DECLARE 
		@DATAHORAREM DATETIME, 
		@DATAULTPROCESSAMENTO DATETIME 
	
	SELECT 
		@DATAHORAREM = GETDATE() 
	
	SELECT @DATAULTPROCESSAMENTO = DATAULTPROCESSAMENTO 
	FROM PARAMETROS 
 
	-- SELECIONANDO TODAS AS MANUTENCOES PREPARADAS PARA ENVIO NO TIPO DE MANUTENCAO VISADO 
	SELECT TOP(@P_QTD) MANUT.*, NULL AS ERRO 
		INTO #TEMP 
	FROM MANUT_TITULOS MANUT 
	WHERE MANUT.TIPOMANUTENCAOTIT = 'D' 
	AND MANUT.SITUACAOPEDIDO = '4' 
	AND CODEVENTO = '8116' 
	AND TIPOENVIO = 'X' 
	AND MANUT.DATAPEDIDO >= @DATAULTPROCESSAMENTO 
 
	/* INCLUINDO A CHAVE DE TODAS AS MANUTENCOES QUE JA NAO ESTIVEREM NA TABELA QUE AUXILIA O WORKFLOW DE ARQUIVOS, 
	JUNTO A MARCACAO DO ARQUIVO QUE ESTA SENDO PROCESSADO E O TIMESTAMP DESSA EXECUCAO */ 
	DELETE #TEMP 
	FROM #TEMP, 
		TEMP_CANCELAMENTO_BAIXA_TITULO_GERACAO_ARQUIVO  
	WHERE #TEMP.DATAPEDIDO = TEMP_CANCELAMENTO_BAIXA_TITULO_GERACAO_ARQUIVO.DATAPEDIDO 
	AND #TEMP.DATALEGADO = TEMP_CANCELAMENTO_BAIXA_TITULO_GERACAO_ARQUIVO.DATALEGADO 
	AND #TEMP.NUMCTRLLEGADO = TEMP_CANCELAMENTO_BAIXA_TITULO_GERACAO_ARQUIVO.NUMCTRLLEGADO 
	AND #TEMP.CODSISLEGADO = TEMP_CANCELAMENTO_BAIXA_TITULO_GERACAO_ARQUIVO.CODSISLEGADO 
 
	INSERT INTO TEMP_CANCELAMENTO_BAIXA_TITULO_GERACAO_ARQUIVO (
		DATAPEDIDO, DATALEGADO, CODSISLEGADO, NUMCTRLLEGADO, 
		NOMEARQREM, DATAHORAREM
	) 
	SELECT 
		DATAPEDIDO, DATALEGADO, CODSISLEGADO, NUMCTRLLEGADO, 
		@P_NOMEARQREM, @DATAHORAREM 
	FROM #TEMP 
 
	/* PROVAVELMENTE ESSA MESMA PROCEDURE SENDO EXECUTADA QUASE SIMULTANEAMENTE, GERANDO PK DUPLICADA... 
	O WORKFLOW DEVE SER SUSPENSO COM O RETORNO DE UM ERRO */ 
	IF @@ERROR != 0 
	BEGIN 
		SELECT 'S' ERRO 
	RETURN 
	END 
 
	-- ESSA TEMP EH UM ESPELHO DESSAS MANUTENCOES QUE ACABARAM DE SER RESERVADAS. RETORNAR PARA QUE SEJAM UTILIZADAS NA GERACAO DO ARQUIVO 
	SELECT * 
	FROM #TEMP 
END 
GO 

INSERT INTO VERSAO_SISTEMA (
	[VERSAO],
	[NOMESCRIPT],
	[CODUSUARIO],
	[DATAATU]
)
SELECT 
	'V15_01_1_01B', 
	'SP_RESERVAR_CANCELAMENTO_BAIXA_TITULO_DESTINATARIA_GERACAO_ARQUIVO',
	SYSTEM_USER, 
	GETDATE() 
GO