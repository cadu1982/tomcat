USE AB_DDA
GO

IF NOT EXISTS (SELECT * FROM sys.objects WHERE name = 'SP_PROCESSA_DDA0101_ADDA101_RR2')
BEGIN
	EXEC ('CREATE PROCEDURE dbo.SP_PROCESSA_DDA0101_ADDA101_RR2 AS BEGIN RETURN END')
END
GO

ALTER PROCEDURE dbo.SP_PROCESSA_DDA0101_ADDA101_RR2(
			@P_TIMEOUT INT,
			@P_SQLERRO INT OUT
) AS BEGIN

	/*
	
        --05/03/2021 - RFAQUINI - CAMPO DATAHORAINCLUSAO ADICIONADO PARA INCLUSÕES DE NOVOS TÍTULOS
	/* 08/03/2023 - Eliane - Corrigindo o UPDATE final para pegar apenas CODPROCESSA 4 E 5! Estava pegando tudo, até o 1! */                                     

		
		MARCACOES DIFERENCIADAS (CODPROCESSA)

		"5" - AVISO JA RECEBIDO COMO R1
		"4" - DUPLICIDADE DE AVISO (APENAS O MAIS RECENTE GERA ATUALIZACAO NA BASE)


		DOMINIO DE REJEICOES (CODREJEICAO)	

		'27' - TITULO JA CADASTRADO

	*/

	DECLARE 
		@DATAULTPROCESSAMENTO DATETIME, 
		@DATAPROCESSAMENTO DATETIME,
		@SQLERRO INTEGER,
		@TIMESTAMP DATETIME,
		@ISPB CHAR(8)

	SELECT 
		@DATAULTPROCESSAMENTO = DATAULTPROCESSAMENTO, 
		@DATAPROCESSAMENTO = DATAPROCESSAMENTO, 
		@TIMESTAMP = GETDATE(), 
		@ISPB = ISPB
   	FROM PARAMETROS 
	WHERE CODSISTEMA = 'DDA-AUTK'  
  	
   	-- TIMEOUT DAS TABELAS DE RESERVA
   	DELETE FROM TEMP_RECEBE_ADDA101_RR2 
	WHERE DATEADD(MINUTE, @P_TIMEOUT, DATAHORAPROC) < @TIMESTAMP

	SELECT @SQLERRO = @@ERROR		 
	IF @SQLERRO <> 0 GOTO TRATAERRO

	-- TRATAMENTO DE RECEBES ACEITAS
	-- SELECIONA TUDO PENDENTE
	SELECT * 
		INTO #TEMP_RR2 
	FROM RECEBE_ADDA101_RR2
	WHERE DATAMOVTO >= @DATAULTPROCESSAMENTO 
	AND DATAMOVTO <= @DATAPROCESSAMENTO 
	AND CODPROCESSA = '0' 
	AND INDIMPORTACAOVALIDA = 'S'

	-- DELETA TUDO QUE ESTA RESERVADO
	DELETE TEMP 
	FROM #TEMP_RR2 TEMP
	INNER JOIN TEMP_RECEBE_ADDA101_RR2 RESERVA 
		ON (RESERVA.SEQ_RECEBE = TEMP.SEQ_RECEBE 
		AND RESERVA.NUMIDENTCDDA = TEMP.NUMIDENTCDDA 
		AND RESERVA.DATAMOVTO = TEMP.DATAMOVTO)

	SELECT @SQLERRO = @@ERROR		 
	IF @SQLERRO <> 0 GOTO TRATAERRO

	-- RESERVA O QUE ESTA LIVRE
	INSERT INTO TEMP_RECEBE_ADDA101_RR2(
		SEQ_RECEBE,NUMIDENTCDDA,DATAMOVTO,DATAHORAPROC
	)
	SELECT SEQ_RECEBE, NUMIDENTCDDA, DATAMOVTO, @TIMESTAMP 
	FROM #TEMP_RR2

	-- PK DUPLICADA, PROCESSOS SIMULTANEOS?   
  	SELECT @SQLERRO = @@ERROR		 
	IF @SQLERRO <> 0 GOTO TRATAERRO
	
	IF(EXISTS(SELECT 1 FROM #TEMP_RR2))
	BEGIN

		/** REJEICOES **/

		-- CODREJEICAO "27" PARA AVISOS DE TITULOS JA CADASTRADOS
		UPDATE #TEMP_RR2 SET 
			CODREJEICAO = '27', 
			CODPROCESSA = '2' 
		FROM #TEMP_RR2 TEMP
		INNER JOIN TITULOS 
			ON (TEMP.CODPROCESSA = '0' 
			AND TEMP.CODREJEICAO IS NULL
			AND TITULOS.NUMIDENTCDDA = TEMP.NUMIDENTCDDA)

		SELECT @SQLERRO = @@ERROR		 
		IF @SQLERRO <> 0 GOTO TRATAERRO

		/** MARCACOES DIFERENCIADAS **/
		-- MOVIMENTOS SERAO SEPARADOS DOS DEMAIS PARA SOMENTE RECEBEREM A MARCACAO DE PROCESSADOS AO FINAL DA PROCEDURE

		-- CODPROCESSA "5": INCLUSAO JA RECEBIDA VIA ADDA/DDA0101R1
		UPDATE #TEMP_RR2 SET 
			CODPROCESSA = '4'  
		FROM #TEMP_RR2 TEMP
		WHERE TEMP.CODPROCESSA = '0' 
		AND TEMP.CODREJEICAO IS NULL 
		AND ((TEMP.ISPBIFCEDENTE = @ISPB) -- SE A R1 AINDA NAO CHEGOU, DEVERA CHEGAR EM BREVE: A IF QUE INCLUIU O TITULO FOI A PROPRIA INSTITUICAO
			OR EXISTS (SELECT 1 
						FROM RECEBE_ADDA101_RET_ACEITOS
						WHERE DATAMOVTO >= @DATAULTPROCESSAMENTO 
						AND DATAMOVTO <= @DATAPROCESSAMENTO 
						AND NUMCTRLDDA = TEMP.NUMCTRLDDA) -- R1 JA CHEGOU E TEM O MESMO NUMCTRLDDA
			)

		SELECT @SQLERRO = @@ERROR		 
		IF @SQLERRO <> 0 GOTO TRATAERRO

		-- CODPROCESSA "4": DUPLICIDADE DE AVISOS
		-- UTILIZANDO APENAS O RECEBIMENTO MAIS RECENTE PARA CADA TITULO EM TERMOS DE DATAHORADDA E SEQ_RECEBE
		-- O RESTO SERA MARCADO PARA FICAR COM STATUS PROCESSADO AO FINAL
		UPDATE #TEMP_RR2 SET 
			CODPROCESSA = '4'  
		FROM #TEMP_RR2 TEMP
		WHERE TEMP.CODPROCESSA = '0' 
		AND TEMP.CODREJEICAO IS NULL 
		AND TEMP.DATAHORADDA < (SELECT MAX(DATAHORADDA) 
								FROM #TEMP_RR2 
								WHERE CODPROCESSA = '0' 
								AND CODREJEICAO IS NULL 
								AND NUMIDENTCDDA = TEMP.NUMIDENTCDDA)

		SELECT @SQLERRO = @@ERROR		 
		IF @SQLERRO <> 0 GOTO TRATAERRO

		UPDATE #TEMP_RR2 SET 
			CODPROCESSA = '4'  
		FROM #TEMP_RR2 TEMP
		WHERE TEMP.CODPROCESSA = '0' 
		AND TEMP.CODREJEICAO IS NULL 
		AND SEQ_RECEBE < (SELECT MAX(SEQ_RECEBE) 
							FROM #TEMP_RR2 
							WHERE CODPROCESSA = '0' 
							AND CODREJEICAO IS NULL 
							AND NUMIDENTCDDA = TEMP.NUMIDENTCDDA)

		SELECT @SQLERRO = @@ERROR		 
		IF @SQLERRO <> 0 GOTO TRATAERRO

		-- O RESTO PODE SER DADO COMO ACEITO E PROCESSADO
		UPDATE #TEMP_RR2 SET 
			CODPROCESSA = '1', 
			DATAMANUTENCAO = @TIMESTAMP 
		WHERE CODPROCESSA = '0'
		AND CODREJEICAO IS NULL


		SELECT @SQLERRO = @@ERROR		 
		IF @SQLERRO <> 0 GOTO TRATAERRO

		--TITULOS NOVOS
		BEGIN TRANSACTION

			--INCLUSAO			
			INSERT INTO TITULOS (
				NUMIDENTCDDA, ISPBCEDENTE, CODIFCEDENTE, TIPOPESCEDENTE, CNPJ_CPF_CEDENTE, NOMECEDENTE, NOMEFANTASIACEDENTE, TIPOPESCEDENTEORI,
				CNPJ_CPF_CEDENTEORI, NOMECEDENTEORI, NOMEFANTASIACEDENTEORI, ENDCEDENTEORI, CIDCEDENTEORI, UFCEDENTEORI, CEPCEDENTEORI, TIPOPESSACADO, CNPJ_CPF_SACADO, NOMESACADO,
				NOMEFANTASIASACADO, ENDSACADO, CIDSACADO, UFSACADO, CEPSACADO, TIPOPESSACADOR, CNPJ_CPF_SACADOR, NOMESACADOR, CODCARTEIRA, CODMOEDACNAB, IDENTNOSSONUMERO, NUMCODBARRAS,
				LINHADIGITAVEL, DATAVENCIMENTO, VLRTITULO, SEUNUMERO, CODESPECIEDOC, DATAEMISSAO, QTDEDIASPROTESTO, DATALIMPAGTO, TIPOPAGTO, NUMPARCELA, QTDPARCELA, INDTITULONEGOCIADO,
				INDBLOQUEIO, INDPARCIAL, QTDPAGTO, VLRABATIMENTO, CODMORA, DATAMORA, VLRPERCMORA, CODMULTA, DATAMULTA, VLRPERCMULTA, CODDESCONTO01, DATADESCONTO01, VLRPERCDESCONTO01,
				CODDESCONTO02, DATADESCONTO02, VLRPERCDESCONTO02, CODDESCONTO03, DATADESCONTO03, VLRPERCDESCONTO03, INDVALORPERC_MIN, VLRMINTITULO, INDVALORPERC_MAX, VLRMAXTITULO,
				TIPOCALCULO, TIPOAUTRECDIVERGENTE, VLRSALDOATUAL, CODSITUACAO, DTHRSITTITULO, ULTNUMREFCADTIT, ULTNUMSEQCADTIT, DATAINCLUSAO, DATAHORADDA, SITPAGAMENTOCIP,
				DATAHORAINCLUSAO--05/03/2021 - RFAQUINI - CAMPO DATAHORAINCLUSAO ADICIONADO PARA INCLUSÕES DE NOVOS TÍTULOS
			)

			SELECT	
				INCLUSAO.NUMIDENTCDDA,  INCLUSAO.ISPBIFCEDENTE, INCLUSAO.CODIFCEDENTE, INCLUSAO.TIPOPESCEDENTE, INCLUSAO.CNPJ_CPF_CEDENTE, INCLUSAO.NOMECEDENTE, INCLUSAO.NOMEFANTASIACEDENTE, 
				INCLUSAO.TIPOPESCEDENTEORI, INCLUSAO.CNPJ_CPF_CEDENTEORI, INCLUSAO.NOMECEDENTEORI, INCLUSAO.NOMEFANTASIACEDENTEORI, INCLUSAO.ENDCEDENTEORI, INCLUSAO.CIDCEDENTEORI,
				INCLUSAO.UFCEDENTEORI, INCLUSAO.CEPCEDENTEORI, INCLUSAO.TIPOPESSACADO, INCLUSAO.CNPJ_CPF_SACADO, INCLUSAO.NOMESACADO, INCLUSAO.NOMEFANTASIASACADO, INCLUSAO.ENDSACADO,
				INCLUSAO.CIDSACADO, INCLUSAO.UFSACADO, INCLUSAO.CEPSACADO, INCLUSAO.TIPOPESSACADOR, INCLUSAO.CNPJ_CPF_SACADOR, INCLUSAO.NOMESACADOR, INCLUSAO.CODCARTEIRA, INCLUSAO.CODMOEDACNAB,
				INCLUSAO.IDENTNOSSONUMERO, INCLUSAO.NUMCODBARRAS, INCLUSAO.LINHADIGITAVEL, INCLUSAO.DATAVENCIMENTO, INCLUSAO.VLRTITULO, INCLUSAO.SEUNUMERO, INCLUSAO.CODESPECIEDOC, INCLUSAO.DATAEMISSAO,
				INCLUSAO.QTDEDIASPROTESTO, INCLUSAO.DATALIMPAGTO, INCLUSAO.TIPOPAGTO, INCLUSAO.NUMPARCELA, INCLUSAO.QTDPARCELA, INCLUSAO.INDTITULONEGOCIADO, INCLUSAO.INDBLOQUEIO, INCLUSAO.INDPARCIAL,
				INCLUSAO.QTDPAGTO, INCLUSAO.VLRABATIMENTO, INCLUSAO.CODMORA, INCLUSAO.DATAMORA, INCLUSAO.VLRPERCMORA, INCLUSAO.CODMULTA, INCLUSAO.DATAMULTA, INCLUSAO.VLRPERCMULTA, INCLUSAO.CODDESCONTO01,
				INCLUSAO.DATADESCONTO01, INCLUSAO.VLRPERCDESCONTO01, INCLUSAO.CODDESCONTO02, INCLUSAO.DATADESCONTO02, INCLUSAO.VLRPERCDESCONTO02, INCLUSAO.CODDESCONTO03, INCLUSAO.DATADESCONTO03,
				INCLUSAO.VLRPERCDESCONTO03, INCLUSAO.INDVALORPERC_MIN, INCLUSAO.VLRMINTITULO, INCLUSAO.INDVALORPERC_MAX, INCLUSAO.VLRMAXTITULO, INCLUSAO.TIPOMODELOCALCULO, INCLUSAO.TIPOAUTRECDIVERGENTE,
				INCLUSAO.VLRSALDOATUAL, SUBSTRING(INCLUSAO.CODSITUACAO, 2, 1), INCLUSAO.DATAHORASITUACAO, INCLUSAO.NUMREFCADTIT, INCLUSAO.NUMSEQCADTIT, INCLUSAO.DATAMOVTO, INCLUSAO.DATAHORADDA, INCLUSAO.SITPAGTO,
				@TIMESTAMP--05/03/2021 - RFAQUINI - CAMPO DATAHORAINCLUSAO ADICIONADO PARA INCLUSÕES DE NOVOS TÍTULOS

			FROM #TEMP_RR2 INCLUSAO 
			WHERE INCLUSAO.CODPROCESSA = '1' 
			AND CODREJEICAO IS NULL
	
			SELECT @SQLERRO = @@ERROR		 
			IF @SQLERRO <> 0 GOTO TRATAERRO_TRANS
	
			DELETE C FROM CALCULOS C 
			INNER JOIN #TEMP_RR2 SELECAO
				ON (SELECAO.CODPROCESSA = '1' 
				AND SELECAO.CODREJEICAO IS NULL 
				AND C.NUMIDENTCDDA = SELECAO.NUMIDENTCDDA)
			
			SELECT @SQLERRO = @@ERROR		 
			IF @SQLERRO <> 0 GOTO TRATAERRO_TRANS
	
			INSERT INTO CALCULOS (
				NUMIDENTCDDA, DATACALCULO, VLRMORA, VLRMULTA, VLRDESCONTO, VLRCOBRAR 
			)
			SELECT 
				CALCULOS_RR2.NUMIDENTCDDA, CALCULOS_RR2.DATAVALCALC, CALCULOS_RR2.VLRJUROS, CALCULOS_RR2.VLRMULTA, CALCULOS_RR2.VLRDESC, CALCULOS_RR2.VLRCOBRAR
			FROM RECEBE_CALCULOS_ADDA101_RR2 CALCULOS_RR2
			INNER JOIN #TEMP_RR2 SELECAO
				ON (SELECAO.CODPROCESSA = '1' 
				AND SELECAO.CODREJEICAO IS NULL 
				AND CALCULOS_RR2.SEQ_RECEBE = SELECAO.SEQ_RECEBE)
	
			SELECT @SQLERRO = @@ERROR		 
			IF @SQLERRO <> 0 GOTO TRATAERRO_TRANS

			DELETE MENSAGENS
				FROM MENSAGENS MENSAGENS 
					INNER JOIN #TEMP_RR2 SELECAO
						ON (SELECAO.CODPROCESSA = '1' AND SELECAO.CODREJEICAO IS NULL AND MENSAGENS.NUMIDENTCDDA = SELECAO.NUMIDENTCDDA)
			
			SELECT @SQLERRO = @@ERROR
			IF @SQLERRO <> 0 GOTO TRATAERRO_TRANS

			INSERT INTO MENSAGENS(
				NUMIDENTCDDA, SEQMENSAGEM, TEXTO
			)
			SELECT 
				MENSAGENS_RR2.NUMIDENTCDDA, MENSAGENS_RR2.SEQMENSAGEM, MENSAGENS_RR2.TEXTO
			FROM RECEBE_MENSAGENS_ADDA101_RR2 MENSAGENS_RR2
			INNER JOIN #TEMP_RR2 SELECAO
				ON (SELECAO.CODPROCESSA = '1' 
				AND SELECAO.CODREJEICAO IS NULL 
				AND MENSAGENS_RR2.SEQ_RECEBE = SELECAO.SEQ_RECEBE)
	
			SELECT @SQLERRO = @@ERROR		 
			IF @SQLERRO <> 0 GOTO TRATAERRO_TRANS		

			DELETE NOTAS_FISCAIS
				FROM NOTAS_FISCAIS NOTAS_FISCAIS
					INNER JOIN #TEMP_RR2 SELECAO
						ON (SELECAO.CODPROCESSA = '1' AND SELECAO.CODREJEICAO IS NULL AND NOTAS_FISCAIS.NUMIDENTCDDA = SELECAO.NUMIDENTCDDA)
			
			SELECT @SQLERRO = @@ERROR
			IF @SQLERRO <> 0 GOTO TRATAERRO_TRANS

			INSERT INTO NOTAS_FISCAIS(
				NUMIDENTCDDA, DATAEMISSAO, NUMNOTAFISCAL, VLRNOTAFISCAL
			)
			SELECT 
				NOTAS_FISCAIS_RR2.NUMIDENTCDDA, NOTAS_FISCAIS_RR2.DATAEMISSAO, NOTAS_FISCAIS_RR2.NUMNOTAFISCAL, NOTAS_FISCAIS_RR2.VLRNOTAFISCAL
			FROM RECEBE_NOTAS_FISCAIS_ADDA101_RR2 NOTAS_FISCAIS_RR2
			INNER JOIN #TEMP_RR2 SELECAO
				ON (SELECAO.CODPROCESSA = '1' 
				AND SELECAO.CODREJEICAO IS NULL 
				AND NOTAS_FISCAIS_RR2.SEQ_RECEBE = SELECAO.SEQ_RECEBE)
	
			SELECT @SQLERRO = @@ERROR		 
			IF @SQLERRO <> 0 GOTO TRATAERRO_TRANS

			-- RESTABELECENDO OS RECEBIMENTOS COM MARCACOES DIFERENCIADAS PARA O ESTADO DE PROCESSADO
			UPDATE #TEMP_RR2 SET 
				CODPROCESSA = '2', /* 08/03/2023 - Estava voltando para 1 */
				DATAMANUTENCAO = @TIMESTAMP  
			WHERE CODPROCESSA IN ('4', '5')

			SELECT @SQLERRO = @@ERROR		 
			IF @SQLERRO <> 0 GOTO TRATAERRO

			UPDATE RECEBE SET 
				CODPROCESSA = TEMP.CODPROCESSA, 
				CODREJEICAO = TEMP.CODREJEICAO, 
				DATAMANUTENCAO = TEMP.DATAMANUTENCAO
			FROM RECEBE_ADDA101_RR2 RECEBE
			INNER JOIN #TEMP_RR2 TEMP 
				ON (RECEBE.SEQ_RECEBE = TEMP.SEQ_RECEBE)

			SELECT @SQLERRO = @@ERROR		 
			IF @SQLERRO <> 0 GOTO TRATAERRO_TRANS

		COMMIT TRANSACTION
	
	END
RETURN
TRATAERRO_TRANS:
	ROLLBACK TRANSACTION
	GOTO TRATAERRO
TRATAERRO:
	SELECT @P_SQLERRO = @SQLERRO
	RETURN
END


GO

INSERT INTO VERSAO_SISTEMA (
	[VERSAO],
	[NOMESCRIPT],
	[CODUSUARIO],
	[DATAATU])
SELECT 
	'V15_01_1_01B', 
	'SP_PROCESSA_DDA0101_ADDA101_RR2', 
	SYSTEM_USER, 
	GETDATE() 
GO

