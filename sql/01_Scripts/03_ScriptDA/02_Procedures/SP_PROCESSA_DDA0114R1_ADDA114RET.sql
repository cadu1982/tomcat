USE AB_DDA
GO

IF NOT EXISTS (SELECT * FROM sys.objects WHERE name = 'SP_PROCESSA_DDA0114R1_ADDA114RET')
BEGIN
	EXEC ('CREATE PROCEDURE dbo.SP_PROCESSA_DDA0114R1_ADDA114RET AS BEGIN RETURN END')
END
GO

ALTER PROCEDURE dbo.SP_PROCESSA_DDA0114R1_ADDA114RET(
	@P_TIMEOUT INT,
	@P_SQLERRO INT OUT
) AS BEGIN

	/*
		25/05/2022 - RFAQUINI - INCLUSÃO E TRATAMENTO DO CAMPO DATAHORARECBTTITULO (CATÁLOGO 5.04)
								REMOÇÃO DE ALIAS
	
		28/06/2022 - RFAQUINI - INCLUSÃO E TRATAMENTO DO CAMPO NOME_PORTADOR (CATÁLOGO 5.03)
		24/02/2023 - ELIANE   - ADEQUAÇÕES PARA A MODERNIZAÇÃO.
	*/

	/*
		MARCACOES DIFERENCIADAS (CODPROCESSA)

		"4" - DUPLICIDADE DE RECEBIMENTO (APENAS O MAIS RECENTE GERA ATUALIZACAO NA BASE)

		DOMINIOS DE REJEICOES (CODREJEICAO)

		'06' - PEDIDO CORRESPONDENTE NAO ENCONTRADO
		'01' - MANUTENCAO DE TITULO NAO CADASTRADO
		'26' - BAIXA JA CADASTRADA

	*/

	DECLARE 
		@DATAULTPROCESSAMENTO DATETIME, 
		@DATAPROCESSAMENTO DATETIME, 
		@TIMESTAMP DATETIME, 
		@SQLERRO INTEGER,
		@VRSMANUAL VARCHAR (07) /* 24/02/2023 */

	
	SELECT 
		@DATAULTPROCESSAMENTO = DATAULTPROCESSAMENTO, 
		@DATAPROCESSAMENTO = DATAPROCESSAMENTO, 
		@TIMESTAMP = GETDATE()
	FROM PARAMETROS 
	WHERE CODSISTEMA = 'DDA-AUTK'
	
	SELECT @VRSMANUAL = VRSMANUAL FROM VWIB_VRSMANUAL WITH (NOLOCK)  /* 24/02/2023 */

	-- DELETA RESERVAS POR TIMEOUT
	DELETE 
	FROM TEMP_RECEBE_ADDA114_RET_RECUSADOS 
	WHERE DATAHORAPROC < DATEADD(MINUTE, -(@P_TIMEOUT), @TIMESTAMP)

	SELECT @SQLERRO = @@ERROR		 
	IF @SQLERRO <> 0 GOTO TRATAERRO

	DELETE 
	FROM TEMP_RECEBE_ADDA114_RET_ACEITOS 
	WHERE DATAHORAPROC < DATEADD(MINUTE, -(@P_TIMEOUT), @TIMESTAMP)

	SELECT @SQLERRO = @@ERROR		 
	IF @SQLERRO <> 0 GOTO TRATAERRO


	-- TRATAMENTO DE RECUSAS	
	-- BUSCA PELO QUE ESTA PENDENTE
	SELECT RECEBE_ADDA114_RET_RECUSADOS.* 
		INTO #TEMP_RECUSADOS 
	FROM RECEBE_ADDA114_RET_RECUSADOS 
	WHERE RECEBE_ADDA114_RET_RECUSADOS.CODPROCESSA = '0' 
	AND RECEBE_ADDA114_RET_RECUSADOS.DATAMOVTO >= @DATAULTPROCESSAMENTO 
	AND RECEBE_ADDA114_RET_RECUSADOS.DATAMOVTO <= @DATAPROCESSAMENTO 
	AND RECEBE_ADDA114_RET_RECUSADOS.INDIMPORTACAOVALIDA = 'S'

	-- REMOVE O QUE AINDA ESTA RESERVADO
	DELETE #TEMP_RECUSADOS 
	FROM #TEMP_RECUSADOS 
	INNER JOIN TEMP_RECEBE_ADDA114_RET_RECUSADOS 
		ON (#TEMP_RECUSADOS.SEQ_RECEBE = TEMP_RECEBE_ADDA114_RET_RECUSADOS.SEQ_RECEBE 
		AND #TEMP_RECUSADOS.NUMPEDIDO = TEMP_RECEBE_ADDA114_RET_RECUSADOS.NUMPEDIDO 
		AND #TEMP_RECUSADOS.DATAMOVTO = TEMP_RECEBE_ADDA114_RET_RECUSADOS.DATAMOVTO)

	SELECT @SQLERRO = @@ERROR		 
	IF @SQLERRO <> 0 GOTO TRATAERRO

	-- RESERVA O QUE ESTIVER LIVRE
	INSERT INTO TEMP_RECEBE_ADDA114_RET_RECUSADOS(
		SEQ_RECEBE, NUMPEDIDO, DATAMOVTO, DATAHORAPROC
	)
	SELECT SEQ_RECEBE, NUMPEDIDO, DATAMOVTO, @TIMESTAMP 
	FROM #TEMP_RECUSADOS

	-- PK DUPLICADA, PROCESSOS SIMULTANEOS?
	SELECT @SQLERRO = @@ERROR		 
	IF @SQLERRO <> 0 GOTO TRATAERRO


	IF EXISTS(SELECT 1 FROM #TEMP_RECUSADOS)
	BEGIN
		-- COD REJEICAO "06" PARA RECEBES SEM MANUTS CORRESPONDENTES
		UPDATE #TEMP_RECUSADOS SET 
			CODREJEICAO = '06', 
			CODPROCESSA = '2' 
		FROM #TEMP_RECUSADOS 
		WHERE NOT EXISTS (
					SELECT 1 
					FROM MANUT_TITULOS 
					WHERE MANUT_TITULOS.SITUACAOPEDIDO = '2' 
					AND MANUT_TITULOS.NUMPEDIDO = #TEMP_RECUSADOS.NUMPEDIDO
					
					/*---- 24/02/2023 - Eliane - Trouxe da proc que processa a 108, essa trava de segurança! ----*/
					AND MANUT_TITULOS.TIPOMANUTENCAOTIT = 'B' 						
				        AND ISNULL(MANUT_TITULOS.NOMEARQRET, '') = ISNULL(#TEMP_RECUSADOS.NOMEARQRET, '')

				 )

		SELECT @SQLERRO = @@ERROR		 
		IF @SQLERRO <> 0 GOTO TRATAERRO

		-- NENHUMA OUTRA REJEICAO, O RESTO PODE SER DADO COMO PROCESSADO E ACEITO
		UPDATE #TEMP_RECUSADOS SET 
			CODPROCESSA = '1' 
		WHERE CODPROCESSA = '0' 
		AND CODREJEICAO IS NULL

		SELECT @SQLERRO = @@ERROR		 
		IF @SQLERRO <> 0 GOTO TRATAERRO
	
		-- APLICANDO RESULTADOS
		BEGIN TRANSACTION			
			-- RECEBES PROCESSADAS
			UPDATE RECEBE_ADDA114_RET_RECUSADOS SET 
				CODPROCESSA = #TEMP_RECUSADOS.CODPROCESSA, 
				CODREJEICAO = #TEMP_RECUSADOS.CODREJEICAO, 
				DATAMANUTENCAO = @TIMESTAMP
			FROM RECEBE_ADDA114_RET_RECUSADOS 
			INNER JOIN #TEMP_RECUSADOS ON 
				(#TEMP_RECUSADOS.SEQ_RECEBE = RECEBE_ADDA114_RET_RECUSADOS.SEQ_RECEBE)

			SELECT @SQLERRO = @@ERROR		 
			IF @SQLERRO <> 0 GOTO TRATAERROTRANS

			-- MANUTS PROCESSADAS
			UPDATE MANUT_TITULOS SET 
				SITUACAOPEDIDO = '5', 
				CODRETORNO = '1'
			FROM MANUT_TITULOS 
			INNER JOIN #TEMP_RECUSADOS
				ON (#TEMP_RECUSADOS.CODPROCESSA = '1' 
				AND MANUT_TITULOS.SITUACAOPEDIDO = '2'
				AND #TEMP_RECUSADOS.NUMPEDIDO = MANUT_TITULOS.NUMPEDIDO)

			SELECT @SQLERRO = @@ERROR		 
			IF @SQLERRO <> 0 GOTO TRATAERROTRANS

		COMMIT TRANSACTION
		
	END

	-- TRATAMENTO DE ACEITOS
	-- BUSCA O QUE ESTA PENDENTE
	SELECT RECEBE_ADDA114_RET_ACEITOS.* 
		INTO #TEMP_ACEITOS 
	FROM RECEBE_ADDA114_RET_ACEITOS 
	WHERE RECEBE_ADDA114_RET_ACEITOS.CODPROCESSA = '0' 
	AND RECEBE_ADDA114_RET_ACEITOS.DATAMOVTO >= @DATAULTPROCESSAMENTO 
	AND RECEBE_ADDA114_RET_ACEITOS.DATAMOVTO <= @DATAPROCESSAMENTO 
	AND RECEBE_ADDA114_RET_ACEITOS.INDIMPORTACAOVALIDA = 'S'

	-- REMOVE O QUE AINDA ESTA RESERVADO
	DELETE #TEMP_ACEITOS
	FROM #TEMP_ACEITOS
	INNER JOIN TEMP_RECEBE_ADDA114_RET_ACEITOS  
		ON (#TEMP_ACEITOS.SEQ_RECEBE = TEMP_RECEBE_ADDA114_RET_ACEITOS.SEQ_RECEBE 
		AND #TEMP_ACEITOS.NUMPEDIDO = TEMP_RECEBE_ADDA114_RET_ACEITOS.NUMPEDIDO
		AND #TEMP_ACEITOS.DATAMOVTO = TEMP_RECEBE_ADDA114_RET_ACEITOS.DATAMOVTO)

	SELECT @SQLERRO = @@ERROR		 
	IF @SQLERRO <> 0 GOTO TRATAERRO

	-- RESERVA O QUE ESTA LIVRE
	INSERT INTO TEMP_RECEBE_ADDA114_RET_ACEITOS(
		SEQ_RECEBE, NUMPEDIDO, DATAMOVTO, DATAHORAPROC
	)
	SELECT 
		#TEMP_ACEITOS.SEQ_RECEBE, #TEMP_ACEITOS.NUMPEDIDO, #TEMP_ACEITOS.DATAMOVTO, @TIMESTAMP 
	FROM #TEMP_ACEITOS 

	-- PK DUPLICADA, PROCESSOS SIMULTANEOS?
	SELECT @SQLERRO = @@ERROR		 
	IF @SQLERRO <> 0 GOTO TRATAERRO

	IF EXISTS(SELECT 1 FROM #TEMP_ACEITOS)
	BEGIN
		-- CRIANDO UMA TABELA DE TRABALHO DE MANUTS
		SELECT MANUT_TITULOS.* 
			INTO #TEMP_MANUT 
		FROM MANUT_TITULOS 
		WHERE MANUT_TITULOS.SITUACAOPEDIDO = '2' 
		AND MANUT_TITULOS.NUMPEDIDO IN (SELECT NUMPEDIDO FROM #TEMP_ACEITOS)

		/** REJEICOES **/

		-- COD REJEICAO "06" PARA RECEBES SEM MANUT CORRESPONDENTE
		UPDATE #TEMP_ACEITOS SET 
			CODREJEICAO = '06', 
			CODPROCESSA = '2' 
		FROM #TEMP_ACEITOS 
		WHERE CODPROCESSA = '0' 
		AND CODREJEICAO IS NULL 
		AND NOT EXISTS (
				SELECT 1 
				FROM MANUT_TITULOS  
				WHERE MANUT_TITULOS.SITUACAOPEDIDO = '2' 
				AND #TEMP_ACEITOS.NUMPEDIDO = MANUT_TITULOS.NUMPEDIDO
			       )

		SELECT @SQLERRO = @@ERROR		 
		IF @SQLERRO <> 0 GOTO TRATAERRO

		-- COD REJEICAO "12" PARA PEDIDOS DE UM TITULO NAO CADASTRADO
		UPDATE #TEMP_ACEITOS SET 
			CODREJEICAO = '12', 
			CODPROCESSA = '2' 
		FROM #TEMP_ACEITOS 
		WHERE CODPROCESSA = '0' 
		AND CODREJEICAO IS NULL 
		AND NOT EXISTS (
				SELECT 1 
				FROM TITULOS 
				WHERE TITULOS.NUMIDENTCDDA = #TEMP_ACEITOS.NUMIDENTCDDA
			       )

		SELECT @SQLERRO = @@ERROR		 
		IF @SQLERRO <> 0 GOTO TRATAERRO
		
		-- COD REJEICAO "26" PARA BAIXAS JA CADASTRADAS
		UPDATE #TEMP_ACEITOS SET 
			CODREJEICAO = '26', 
			CODPROCESSA = '2' 
		FROM #TEMP_ACEITOS 
		INNER JOIN BAIXAS_OPERACIONAIS 
			ON (#TEMP_ACEITOS.CODPROCESSA = '0' 
			AND #TEMP_ACEITOS.CODREJEICAO IS NULL 
			AND BAIXAS_OPERACIONAIS.NUMIDENTCDDA = #TEMP_ACEITOS.NUMIDENTCDDA 
			AND BAIXAS_OPERACIONAIS.NUMIDENTCBAIXAOPERAC = #TEMP_ACEITOS.NUMIDENTCBAIXAOPE)

		SELECT @SQLERRO = @@ERROR		 
		IF @SQLERRO <> 0 GOTO TRATAERRO

		/** MARCACOES DIFERENCIADAS **/
		-- MOVIMENTOS SERAO SEPARADOS DOS DEMAIS PARA SOMENTE RECEBEREM A MARCACAO DE PROCESSADOS AO FINAL DA PROCEDURE

		-- CODPROCESSA "4": DUPLICIDADE DE MOVIMENTOS
		-- SE HOUVER DUPLICIDADE NO PEDIDO, MESMA BAIXA, OS RECEBIMENTOS COM DATAHORADDA OU SEQ_RECEBE MENOR SAO SEPARADAS PARA SEREM SOMENTE MARCADAS COMO PROCESSADAS AO FINAL
		
		SELECT MAX(#TEMP_ACEITOS.DATAHORADDA) MAX_DATAHORADDA, #TEMP_ACEITOS.NUMIDENTCBAIXAOPE, #TEMP_ACEITOS.NUMIDENTCDDA 
			INTO #TEMP_MAX_DATAHORADDA 
		FROM #TEMP_ACEITOS 
		WHERE #TEMP_ACEITOS.CODPROCESSA = '0' 
		AND #TEMP_ACEITOS.CODREJEICAO IS NULL 
		GROUP BY  #TEMP_ACEITOS.NUMIDENTCDDA, #TEMP_ACEITOS.NUMIDENTCBAIXAOPE

		UPDATE #TEMP_ACEITOS SET 
			CODPROCESSA = '4' 
		FROM #TEMP_ACEITOS 
		WHERE #TEMP_ACEITOS.CODPROCESSA = '0' 
		AND #TEMP_ACEITOS.CODREJEICAO IS NULL 
		AND #TEMP_ACEITOS.DATAHORADDA < (
						  SELECT MAX_DATAHORADDA 
						  FROM #TEMP_MAX_DATAHORADDA 
						  WHERE #TEMP_MAX_DATAHORADDA.NUMIDENTCBAIXAOPE = #TEMP_ACEITOS.NUMIDENTCBAIXAOPE
						)
		
		SELECT @SQLERRO = @@ERROR		 
		IF @SQLERRO <> 0 GOTO TRATAERRO

		SELECT MAX(#TEMP_ACEITOS.SEQ_RECEBE) MAX_SEQ_RECEBE, #TEMP_ACEITOS.NUMIDENTCBAIXAOPE, #TEMP_ACEITOS.NUMIDENTCDDA 
			INTO #TEMP_MAX_SEQ_RECEBE 
		FROM #TEMP_ACEITOS 
		WHERE #TEMP_ACEITOS.CODPROCESSA = '0' 
		AND #TEMP_ACEITOS.CODREJEICAO IS NULL
		GROUP BY #TEMP_ACEITOS.NUMIDENTCDDA, #TEMP_ACEITOS.NUMIDENTCBAIXAOPE
		
		UPDATE #TEMP_ACEITOS SET 
			CODPROCESSA = '4' 
		FROM #TEMP_ACEITOS
		WHERE #TEMP_ACEITOS.CODPROCESSA = '0' 
		AND #TEMP_ACEITOS.CODREJEICAO IS NULL 
		AND #TEMP_ACEITOS.SEQ_RECEBE < (
						SELECT MAX_SEQ_RECEBE 
						FROM #TEMP_MAX_SEQ_RECEBE 
						WHERE #TEMP_MAX_SEQ_RECEBE.NUMIDENTCBAIXAOPE = #TEMP_ACEITOS.NUMIDENTCBAIXAOPE
					       )

		SELECT @SQLERRO = @@ERROR		 
		IF @SQLERRO <> 0 GOTO TRATAERRO

		-- SEM MAIS REJEICOES, O RESTO PODE SER DADO COMO ACEITO E PROCESSADO
		UPDATE #TEMP_ACEITOS SET 
			CODPROCESSA = '1' 
		WHERE CODPROCESSA = '0' 
		AND CODREJEICAO IS NULL

		SELECT @SQLERRO = @@ERROR		 
		IF @SQLERRO <> 0 GOTO TRATAERRO

		-- MARCANDO ANTECIPADAMENTE COMO PROCESSADO E COM O COD RETORNO CORRESPONDENTE ("0" SE NAO HOUVER REJEICAO E "1" CASO CONTRARIO)
		UPDATE #TEMP_MANUT SET 
			SITUACAOPEDIDO = '5', 
			CODRETORNO = '1' 
		FROM #TEMP_MANUT 
		INNER JOIN #TEMP_ACEITOS 
			ON (#TEMP_ACEITOS.CODPROCESSA = '2' 
			AND #TEMP_ACEITOS.CODREJEICAO IS NOT NULL 
			AND #TEMP_ACEITOS.NUMPEDIDO = #TEMP_MANUT.NUMPEDIDO)

		SELECT @SQLERRO = @@ERROR		 
		IF @SQLERRO <> 0 GOTO TRATAERRO

		UPDATE #TEMP_MANUT 
		SET SITUACAOPEDIDO = '5', 
			CODRETORNO = '0',
			NUMREFBAIXAOPERAC_RET = #TEMP_ACEITOS.NUMREFBAIXAOPE,
			NUMSEQBAIXAOPERAC     = #TEMP_ACEITOS.NUMSEQBAIXAOPE 
		FROM #TEMP_MANUT 
		INNER JOIN #TEMP_ACEITOS 
			ON (#TEMP_ACEITOS.CODPROCESSA = '1' 
			AND #TEMP_ACEITOS.CODREJEICAO IS NULL 
			AND #TEMP_ACEITOS.NUMPEDIDO = #TEMP_MANUT.NUMPEDIDO)

		SELECT @SQLERRO = @@ERROR		 
		IF @SQLERRO <> 0 GOTO TRATAERRO
		
		-- APLICANDO RESULTADOS
		BEGIN TRANSACTION			
	
	          IF @VRSMANUAL <= '5.05'  
	                
	             BEGIN /* Catalogo anterior à MODERNIZAÇÃO - SOMENTE AVISO DE LIQUIDAÇÃO */

			UPDATE TITULOS SET 
				SITPAGAMENTO = '2', /* SITUACAO "BAIXA ENVIADA" */
				ULTNUMREFBAIXAOPERAC = #TEMP_ACEITOS.NUMREFBAIXAOPE,
				ULTNUMSEQBAIXAOPERAC = #TEMP_ACEITOS.NUMSEQBAIXAOPE,
				INDCONTINGENCIA = #TEMP_MANUT.INDCONTINGENCIA,
				TIPOPESPORTADOR	= #TEMP_MANUT.TIPOPESPORTADOR,
				CNPJ_CPF_PORTADOR = #TEMP_MANUT.CNPJ_CPF_PORTADOR,
				NUMCODBARRAS = #TEMP_ACEITOS.NUMCODBARRASBAIXAOPE,
				ULTNUMREFCADTIT = #TEMP_ACEITOS.NUMREFCADTIT
			FROM TITULOS
			INNER JOIN #TEMP_MANUT
				ON (#TEMP_MANUT.CODRETORNO = '0' 
				AND #TEMP_MANUT.NUMIDENTCDDA = TITULOS.NUMIDENTCDDA)
			INNER JOIN #TEMP_ACEITOS 
				ON (#TEMP_ACEITOS.CODPROCESSA = '1' 
				AND #TEMP_ACEITOS.CODREJEICAO IS NULL 
				AND #TEMP_MANUT.NUMPEDIDO = #TEMP_ACEITOS.NUMPEDIDO)

			SELECT @SQLERRO = @@ERROR		 
			IF @SQLERRO <> 0 GOTO TRATAERROTRANS
			
	             END   /* Catalogo anterior à MODERNIZAÇÃO - SOMENTE AVISO DE LIQUIDAÇÃO */
		 ELSE
	             BEGIN /* Catalogo MODERNIZAÇÃO */

			UPDATE TITULOS SET 
				SITPAGAMENTO = '2', /* SITUACAO "BAIXA ENVIADA" */
				ULTNUMREFBAIXAOPERAC = #TEMP_ACEITOS.NUMREFBAIXAOPE,
				ULTNUMSEQBAIXAOPERAC = ISNULL (#TEMP_ACEITOS.NUMSEQBAIXAOPE, 0),
				INDCONTINGENCIA = #TEMP_MANUT.INDCONTINGENCIA,
				TIPOPESPORTADOR	= #TEMP_MANUT.TIPOPESPORTADOR,
				CNPJ_CPF_PORTADOR = #TEMP_MANUT.CNPJ_CPF_PORTADOR,
				NUMCODBARRAS      = CASE WHEN TITULOS.NUMCODBARRAS IS NULL 
				     			 THEN #TEMP_ACEITOS.NUMCODBARRASBAIXAOPE
				     			 ELSE TITULOS.NUMCODBARRAS  /* fica como está */
						    END,
				ULTNUMREFCADTIT = #TEMP_ACEITOS.NUMREFCADTIT,
				DATAHORADDA   = #TEMP_ACEITOS.DATAHORADDA,
				DTHRSITTITULO = #TEMP_ACEITOS.DATAHORADDA, /* Eliane - coloquei aqui porque a 115 atualiza esse campo tb */
				
/*---------------------------------------------------------------*/				
/* dados que serão atualizados a partir da MODERNIZAÇÃO - inicio */

		                CODSITUACAO = CASE WHEN #TEMP_MANUT.CODEVENTO = '8114' 				AND
		                                        #TEMP_MANUT.TIPOMANUTENCAOTIT =  'B' /* Baixa */	AND
		                                        #TEMP_MANUT.TIPOBAIXA = '0' /* Baixa Operacional Integral Interbancária */
		                                   THEN '4' /* título liquidado - LIQ INTERBANCÁRIA */
		                                   WHEN #TEMP_MANUT.CODEVENTO = '8114' 				AND
		                                        #TEMP_MANUT.TIPOMANUTENCAOTIT =  'B' /* Baixa */	AND
						        #TEMP_MANUT.TIPOBAIXA = '1' /* Baixa Operacional Integral Intrabancária */
		                                   THEN '3' /* título liquidado - LIQ INTRABANCÁRIA */
		                                   WHEN #TEMP_MANUT.CODEVENTO <> '8114' 			AND
		                                   	#TEMP_MANUT.TIPOMANUTENCAOTIT =  'B' /* Baixa */
		                                   THEN #TEMP_MANUT.CODSITUACAO /* Baixa escolhida pela Cobrança */
		                                   ELSE TITULOS.CODSITUACAO /* Continua como está */
		                              END,

                		TIPOBAIXA      = #TEMP_MANUT.TIPOBAIXA, /* Já estará na codificação da modernização */
                		DATAPAGTOBAIXA = #TEMP_MANUT.DATAPAGTOBAIXA,
                		VLRPAGTOBAIXA  = #TEMP_MANUT.VLRPAGTOBAIXA

/* dados que serão atualizados a partir da MODERNIZAÇÃO - fim    */
/*---------------------------------------------------------------*/				
				
			FROM TITULOS
			INNER JOIN #TEMP_MANUT
				ON (#TEMP_MANUT.CODRETORNO = '0' 
				AND #TEMP_MANUT.NUMIDENTCDDA = TITULOS.NUMIDENTCDDA)
			INNER JOIN #TEMP_ACEITOS 
				ON (#TEMP_ACEITOS.CODPROCESSA = '1' 
				AND #TEMP_ACEITOS.CODREJEICAO IS NULL 
				AND #TEMP_MANUT.NUMPEDIDO = #TEMP_ACEITOS.NUMPEDIDO)

			SELECT @SQLERRO = @@ERROR		 
			IF @SQLERRO <> 0 GOTO TRATAERROTRANS
			
	             END   /* Catalogo MODERNIZAÇÃO */
		 

			INSERT INTO BAIXAS_OPERACIONAIS
			(
				NUMIDENTCDDA, NUMIDENTCBAIXAOPERAC, NUMCTRLDDA, NUMREFBAIXAOPERAC,
				NUMSEQBAIXAOPERAC, NUMREFCADTIT, NUMSEQCADTIT, DATAMOVTO,
				DATAHORADDA, TIPOBAIXA, DATABAIXA, DATAHORABAIXA,
				VLRBAIXA, NUMCODBARRAS, SITBAIXA, DATAHORASITUACAO,
				
				ISPBSACADA, CODIFSACADA, NUMCTRLDDACANCEL, DATAHORACANCEL,
				SITPAGTO, SITUACAOTIT, CANALPAGTO, MEIOPAGTO,
				--28/06/2022 - RFAQUINI - INCLUSÃO E TRATAMENTO DO CAMPO NOME_PORTADOR (CATÁLOGO 5.03)
				INDCONTINGENCIA, TIPOPESPORTADOR, CNPJ_CPF_PORTADOR, NOME_PORTADOR,
				--25/05/2022 - RFAQUINI - INCLUSÃO E TRATAMENTO DO CAMPO DATAHORARECBTTITULO (CATÁLOGO 5.04)
				DATAHORARECBTTITULO, QTDPAGTOREG, VLRSALDOATUAL, 
				
				TIPOPESAGREGADOR, CNPJ_CPF_AGREGADOR, NOMEAGREGADOR, ISPBINICIADORPAGTO, 
				AGENCIARECEBEDORA /* CAT MODERNIZAÇÃO */
				
			)
			SELECT
				#TEMP_ACEITOS.NUMIDENTCDDA, #TEMP_ACEITOS.NUMIDENTCBAIXAOPE, #TEMP_ACEITOS.NUMCTRLDDA, #TEMP_ACEITOS.NUMREFBAIXAOPE,
				ISNULL (#TEMP_ACEITOS.NUMSEQBAIXAOPE, 0) /* 14/03/2023 */,#TEMP_ACEITOS.NUMREFCADTIT, #TEMP_ACEITOS.NUMSEQCADTIT, #TEMP_ACEITOS.DATAMOVTO,
				#TEMP_ACEITOS.DATAHORADDA, #TEMP_MANUT.TIPOBAIXA, #TEMP_MANUT.DATAPAGTOBAIXA, #TEMP_MANUT.DATAPAGTOBAIXA,
				#TEMP_MANUT.VLRPAGTOBAIXA, #TEMP_MANUT.NUMCODBARRAS, 'A', /* BAIXA ATIVA */ NULL,
				
				#TEMP_MANUT.ISPBSACADA, #TEMP_MANUT.CODIFSACADA, NULL, NULL,
				NULL, #TEMP_MANUT.CODSITUACAO, #TEMP_MANUT.CANALPAGTO, #TEMP_MANUT.MEIOPAGTO,
				--28/06/2022 - RFAQUINI - INCLUSÃO E TRATAMENTO DO CAMPO NOME_PORTADOR (CATÁLOGO 5.03)
				#TEMP_MANUT.INDCONTINGENCIA, #TEMP_MANUT.TIPOPESPORTADOR, #TEMP_MANUT.CNPJ_CPF_PORTADOR, #TEMP_MANUT.NOME_PORTADOR,
				--25/05/2022 - RFAQUINI - INCLUSÃO E TRATAMENTO DO CAMPO DATAHORARECBTTITULO (CATÁLOGO 5.04)
				#TEMP_MANUT.DATAHORARECBTTITULO, NULL, NULL,
				
				#TEMP_MANUT.TIPOPESAGREGADOR, #TEMP_MANUT.CNPJ_CPF_AGREGADOR, #TEMP_MANUT.NOMEAGREGADOR, #TEMP_MANUT.ISPBINICIADORPAGTO, 
				#TEMP_MANUT.AGENCIARECEBEDORA /* CAT MODERNIZAÇÃO */
				
			FROM #TEMP_ACEITOS 
			INNER JOIN #TEMP_MANUT
				ON (#TEMP_ACEITOS.CODPROCESSA = '1' 
				AND #TEMP_ACEITOS.CODREJEICAO IS NULL 
				AND #TEMP_ACEITOS.NUMPEDIDO = #TEMP_MANUT.NUMPEDIDO)

			SELECT @SQLERRO = @@ERROR		 
			IF @SQLERRO <> 0 GOTO TRATAERROTRANS
			
		
----------------------------------------------------------------		
--ESTE CONTROLE AQUI NÃO PARECE CERTO PORQUE ACABA ENTRANDO NO UPDATE SEGUINTE COM ACEITOS ISOLADOS POR DUPLICIDADES !!!		
----------------------------------------------------------------			
			
			
			-- RESTABELECENDO OS RECEBIMENTOS QUE FORAM MARCADOS PARA PROCESSAMENTO AO FINAL DA PROCEDURE
			UPDATE #TEMP_ACEITOS SET 
				CODPROCESSA = '1' 
			WHERE CODPROCESSA = '4'

			SELECT @SQLERRO = @@ERROR		 
			IF @SQLERRO <> 0 GOTO TRATAERROTRANS
			
			UPDATE #TEMP_MANUT SET 
				SITUACAOPEDIDO = '5', 
				CODRETORNO = '0'
			FROM #TEMP_MANUT 
			INNER JOIN #TEMP_ACEITOS 
				ON (#TEMP_ACEITOS.CODPROCESSA = '1' 
				AND #TEMP_ACEITOS.CODREJEICAO IS NULL 
				AND #TEMP_ACEITOS.NUMPEDIDO = #TEMP_MANUT.NUMPEDIDO)

			SELECT @SQLERRO = @@ERROR		 
			IF @SQLERRO <> 0 GOTO TRATAERROTRANS

			-- RECEBES PROCESSADAS
			UPDATE RECEBE_ADDA114_RET_ACEITOS SET 
				CODPROCESSA = #TEMP_ACEITOS.CODPROCESSA, 
				CODREJEICAO = #TEMP_ACEITOS.CODREJEICAO, 
				DATAMANUTENCAO = @TIMESTAMP
			FROM RECEBE_ADDA114_RET_ACEITOS 
			INNER JOIN #TEMP_ACEITOS  
				ON (#TEMP_ACEITOS.SEQ_RECEBE = RECEBE_ADDA114_RET_ACEITOS.SEQ_RECEBE)

			SELECT @SQLERRO = @@ERROR		 
			IF @SQLERRO <> 0 GOTO TRATAERROTRANS

			-- MANUTS PROCESSADAS
			UPDATE MANUT_TITULOS 
			SET SITUACAOPEDIDO = '5', 
				CODRETORNO = #TEMP_MANUT.CODRETORNO, 
				NUMREFBAIXAOPERAC_RET = #TEMP_MANUT.NUMREFBAIXAOPERAC_RET,
				NUMSEQBAIXAOPERAC = #TEMP_MANUT.NUMSEQBAIXAOPERAC
			FROM MANUT_TITULOS 
			INNER JOIN #TEMP_MANUT 
				ON (MANUT_TITULOS.DATAPEDIDO = #TEMP_MANUT.DATAPEDIDO 
				AND MANUT_TITULOS.DATALEGADO = #TEMP_MANUT.DATALEGADO
				AND MANUT_TITULOS.CODSISLEGADO = #TEMP_MANUT.CODSISLEGADO 
				AND MANUT_TITULOS.NUMCTRLLEGADO = #TEMP_MANUT.NUMCTRLLEGADO)

			SELECT @SQLERRO = @@ERROR		 
			IF @SQLERRO <> 0 GOTO TRATAERROTRANS

		COMMIT TRANSACTION
	END

	RETURN

	TRATAERROTRANS:
		ROLLBACK TRANSACTION
		GOTO TRATAERRO

	TRATAERRO:
		SELECT @P_SQLERRO = @@ERROR
		RETURN

END
GO

INSERT INTO VERSAO_SISTEMA (
	[VERSAO], 
    [NOMESCRIPT], 
    [CODUSUARIO], 
    [DATAATU]
)
SELECT 
	'V15_01_1_01B', 
	'SP_PROCESSA_DDA0114R1_ADDA114RET', 
	SYSTEM_USER, 
	GETDATE() 
GO