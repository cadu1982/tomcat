USE AB_DDA
GO

IF NOT EXISTS (SELECT * FROM sys.objects WHERE name = 'SP_PROCESSA_ADDA121_RR3')
BEGIN
	EXEC ('CREATE PROCEDURE dbo.SP_PROCESSA_ADDA121_RR3 AS BEGIN RETURN END')
END
GO

ALTER PROCEDURE dbo.SP_PROCESSA_ADDA121_RR3(  
	@P_TIMEOUT INT,  
	@P_SQLERRO INT OUT  
) 
AS BEGIN  

	/*
		20/10/2021 - RFAQUINI - INCLUSÃO E TRATAMENTO DO CAMPO NOME_PORTADOR (CATÁLOGO 5.03)
								REMOÇÃO DE ALIAS
								
		25/05/2022 - RFAQUINI - INCLUSÃO E TRATAMENTO DO CAMPO DATAHORARECBTTITULO (CATÁLOGO 5.04)

		21/06/2022 - RFAQUINI - INCLUSÃO DOS CAMPOS TIPOPESAUTORIZADOR E CNPJ_CPF_AUTORIZADOR PARA ATUALIZAÇÃO

		23/02/2023 - ELIANE - TRATAMENTO DOS CAMPOS DO CATÁLOGO MODERNIZAÇÃO.
		                      A alteração não foi grande porque os campos novos serão criados na baixa da versão, mesmo
		                      que estejamos no catálogo 5.05 e poderão ser referenciados nas queries.
		                      Enquanto chegarem BAIXA EFETIVAS, usaremos suas informações para atualizar os títulos.
		                      Depois da MODERNIZAÇÃO, somente usaremos BAIXAS_OPERACIONAIS efetivamente porque as efetivas
		                      não mais serão distribuídas pela CIP.
	*/
	
	
	DECLARE 
		@DATAULTPROCESSAMENTO DATETIME, 
		@DATAPROCESSAMENTO DATETIME, 
		@TIMESTAMP DATETIME, 
		@SQLERRO INT  
	
	SELECT 
		@DATAULTPROCESSAMENTO = DATAULTPROCESSAMENTO, 
		@DATAPROCESSAMENTO = DATAPROCESSAMENTO, 
		@TIMESTAMP = GETDATE()  
	FROM PARAMETROS 
	WHERE CODSISTEMA = 'DDA-AUTK'  

	-- DELETA RESERVAS POR TIMEOUT  
	DELETE FROM TEMP_RECEBE_ADDA121_RR3 
	WHERE DATAHORAPROC < DATEADD(MINUTE, -(@P_TIMEOUT), @TIMESTAMP)  

	SELECT @SQLERRO = @@ERROR     
	IF @SQLERRO <> 0 GOTO TRATAERRO  

	-- BUSCA O QUE ESTA PENDENTE  
	SELECT * 
		INTO #TEMP_TITULOS 
	FROM RECEBE_ADDA121_RR3  
	WHERE RECEBE_ADDA121_RR3.CODPROCESSA = '0' 
	AND RECEBE_ADDA121_RR3.DATAMOVTO >= @DATAULTPROCESSAMENTO 
	AND RECEBE_ADDA121_RR3.DATAMOVTO <= @DATAPROCESSAMENTO   
	AND RECEBE_ADDA121_RR3.INDIMPORTACAOVALIDA = 'S'  

	SELECT @SQLERRO = @@ERROR     
	IF @SQLERRO <> 0 GOTO TRATAERRO  

	-- REMOVE O QUE AINDA ESTA RESERVADO  
	DELETE #TEMP_TITULOS 
	FROM #TEMP_TITULOS   
	INNER JOIN TEMP_RECEBE_ADDA121_RR3  
		ON (TEMP_RECEBE_ADDA121_RR3.SEQ_RECEBE = #TEMP_TITULOS.SEQ_RECEBE  
		AND TEMP_RECEBE_ADDA121_RR3.DATAMOVTO = #TEMP_TITULOS.DATAMOVTO)  

	SELECT @SQLERRO = @@ERROR     
	IF @SQLERRO <> 0 GOTO TRATAERRO  

	-- RESERVA O QUE ESTA LIVRE  
	INSERT INTO TEMP_RECEBE_ADDA121_RR3(
		SEQ_RECEBE, DATAMOVTO, DATAHORAPROC
	)  
	SELECT 
		#TEMP_TITULOS.SEQ_RECEBE, #TEMP_TITULOS.DATAMOVTO, @TIMESTAMP 
	FROM #TEMP_TITULOS   

	-- PK DUPLICADA, PROCESSOS SIMULTANEOS?  
	SELECT @SQLERRO = @@ERROR     
	IF @SQLERRO <> 0 GOTO TRATAERRO  

	-- PREPARACAO DO PROCESSAMENTO  

	-- NADA SERA REJEITADO: O QUE FOR NOVO NA BASE SERA INCLUIDO E O QUE JA EXISTIR SERA ATUALIZADO  
	-- SEPARANDO AS INCLUSÕES DE NOVOS TITULOS DAS ATUALIZACOES DE TITULOS EXISTENTES  
	SELECT NUMIDENTCDDA 
		INTO #TEMP_INCLUSAO 
	FROM #TEMP_TITULOS  
	WHERE NOT EXISTS (SELECT 1 
				FROM TITULOS 
				WHERE TITULOS.NUMIDENTCDDA = #TEMP_TITULOS.NUMIDENTCDDA)  

	SELECT @SQLERRO = @@ERROR     
	IF @SQLERRO <> 0 GOTO TRATAERRO  

	-- TRATAMENTO PARA OUTROS MEMBROS DO TITULO:   

	-- NOTA FISCAL  
	SELECT RECEBE_NOTAS_FISCAIS_ADDA121_RR3.* 
		INTO #TEMP_NOTAS 
	FROM RECEBE_NOTAS_FISCAIS_ADDA121_RR3   
	INNER JOIN #TEMP_TITULOS 
		ON (RECEBE_NOTAS_FISCAIS_ADDA121_RR3.SEQ_RECEBE = #TEMP_TITULOS.SEQ_RECEBE  
		AND RECEBE_NOTAS_FISCAIS_ADDA121_RR3.DATAMOVTO = #TEMP_TITULOS.DATAMOVTO  
		AND RECEBE_NOTAS_FISCAIS_ADDA121_RR3.NUMIDENTCDDA = #TEMP_TITULOS.NUMIDENTCDDA)  

	DELETE #TEMP_NOTAS_1 
	FROM #TEMP_NOTAS #TEMP_NOTAS_1    
	WHERE #TEMP_NOTAS_1.SEQ_RECEBE NOT IN (SELECT MAX(SEQ_RECEBE) 
							FROM #TEMP_NOTAS #TEMP_NOTAS_2
							WHERE #TEMP_NOTAS_1.NUMIDENTCDDA = #TEMP_NOTAS_2.NUMIDENTCDDA)  

	SELECT @SQLERRO = @@ERROR     
	IF @SQLERRO <> 0 GOTO TRATAERRO  

	-- MENSAGEM  
	SELECT RECEBE_MENSAGENS_ADDA121_RR3.* 
		INTO #TEMP_MENSAGENS 
	FROM RECEBE_MENSAGENS_ADDA121_RR3  
	INNER JOIN #TEMP_TITULOS 
		ON (RECEBE_MENSAGENS_ADDA121_RR3.SEQ_RECEBE = #TEMP_TITULOS.SEQ_RECEBE  
		AND RECEBE_MENSAGENS_ADDA121_RR3.DATAMOVTO = #TEMP_TITULOS.DATAMOVTO  
		AND RECEBE_MENSAGENS_ADDA121_RR3.NUMIDENTCDDA = #TEMP_TITULOS.NUMIDENTCDDA)  

	DELETE #TEMP_MENSAGENS_1
	FROM #TEMP_MENSAGENS #TEMP_MENSAGENS_1   
	WHERE #TEMP_MENSAGENS_1.SEQ_RECEBE NOT IN (SELECT MAX(SEQ_RECEBE) 
							FROM #TEMP_MENSAGENS #TEMP_MENSAGENS_2
							WHERE #TEMP_MENSAGENS_1.NUMIDENTCDDA = #TEMP_MENSAGENS_2.NUMIDENTCDDA)  

	SELECT @SQLERRO = @@ERROR     
	IF @SQLERRO <> 0 GOTO TRATAERRO  

	-- CALCULOS  
	SELECT RECEBE_CALCULOS_ADDA121_RR3.* 
		INTO #TEMP_CALCULOS 
	FROM RECEBE_CALCULOS_ADDA121_RR3  
	INNER JOIN #TEMP_TITULOS  
		ON (RECEBE_CALCULOS_ADDA121_RR3.SEQ_RECEBE = #TEMP_TITULOS.SEQ_RECEBE  
		AND RECEBE_CALCULOS_ADDA121_RR3.DATAMOVTO = #TEMP_TITULOS.DATAMOVTO  
		AND RECEBE_CALCULOS_ADDA121_RR3.NUMIDENTCDDA = #TEMP_TITULOS.NUMIDENTCDDA)  

	DELETE #TEMP_CALCULOS_1 
	FROM #TEMP_CALCULOS #TEMP_CALCULOS_1   
	WHERE #TEMP_CALCULOS_1.SEQ_RECEBE NOT IN (SELECT MAX(SEQ_RECEBE) 
							FROM #TEMP_CALCULOS #TEMP_CALCULOS_2
							WHERE #TEMP_CALCULOS_1.NUMIDENTCDDA = #TEMP_CALCULOS_2.NUMIDENTCDDA)  

	SELECT @SQLERRO = @@ERROR     
	IF @SQLERRO <> 0 GOTO TRATAERRO   

	-- TERCEIROS  
	SELECT RECEBE_TERCEIROS_ADDA121_RR3.* 
		INTO #TEMP_TERCEIROS 
	FROM RECEBE_TERCEIROS_ADDA121_RR3   
	INNER JOIN #TEMP_TITULOS  
		ON (RECEBE_TERCEIROS_ADDA121_RR3.SEQ_RECEBE = #TEMP_TITULOS.SEQ_RECEBE  
		AND RECEBE_TERCEIROS_ADDA121_RR3.DATAMOVTO = #TEMP_TITULOS.DATAMOVTO  
		AND RECEBE_TERCEIROS_ADDA121_RR3.NUMIDENTCDDA = #TEMP_TITULOS.NUMIDENTCDDA)  

	SELECT @SQLERRO = @@ERROR     
	IF @SQLERRO <> 0 GOTO TRATAERRO  

	DELETE #TEMP_TERCEIROS_1 
	FROM #TEMP_TERCEIROS #TEMP_TERCEIROS_1
	WHERE #TEMP_TERCEIROS_1.SEQ_RECEBE NOT IN (SELECT MAX(SEQ_RECEBE) 
							FROM #TEMP_TERCEIROS #TEMP_TERCEIROS_2
							WHERE #TEMP_TERCEIROS_1.NUMIDENTCDDA = #TEMP_TERCEIROS_2.NUMIDENTCDDA)  

	SELECT @SQLERRO = @@ERROR     
	IF @SQLERRO <> 0 GOTO TRATAERRO  

	-- BAIXAS OPERACIONAIS  
	SELECT RECEBE_BAIXAS_OPERACIONAIS_ADDA121_RR3.* 
		INTO #TEMP_OPERACIONAIS 
	FROM RECEBE_BAIXAS_OPERACIONAIS_ADDA121_RR3   
	INNER JOIN #TEMP_TITULOS  
		ON (RECEBE_BAIXAS_OPERACIONAIS_ADDA121_RR3.SEQ_RECEBE = #TEMP_TITULOS.SEQ_RECEBE  
		AND RECEBE_BAIXAS_OPERACIONAIS_ADDA121_RR3.DATAMOVTO = #TEMP_TITULOS.DATAMOVTO  
		AND RECEBE_BAIXAS_OPERACIONAIS_ADDA121_RR3.NUMIDENTCDDA = #TEMP_TITULOS.NUMIDENTCDDA)  

	DELETE #TEMP_OPERACIONAIS_1 
	FROM #TEMP_OPERACIONAIS #TEMP_OPERACIONAIS_1   
	WHERE #TEMP_OPERACIONAIS_1.SEQ_RECEBE NOT IN (SELECT MAX(SEQ_RECEBE) 
								FROM #TEMP_OPERACIONAIS #TEMP_OPERACIONAIS_2
								WHERE #TEMP_OPERACIONAIS_1.NUMIDENTCDDA = #TEMP_OPERACIONAIS_2.NUMIDENTCDDA)  

	SELECT @SQLERRO = @@ERROR     
	IF @SQLERRO <> 0 GOTO TRATAERRO  

	-- BAIXAS EFETIVAS  
	SELECT RECEBE_BAIXAS_EFETIVAS_ADDA121_RR3.* 
		INTO #TEMP_EFETIVAS 
	FROM RECEBE_BAIXAS_EFETIVAS_ADDA121_RR3   
	INNER JOIN #TEMP_TITULOS  
		ON (RECEBE_BAIXAS_EFETIVAS_ADDA121_RR3.SEQ_RECEBE = #TEMP_TITULOS.SEQ_RECEBE  
		AND RECEBE_BAIXAS_EFETIVAS_ADDA121_RR3.DATAMOVTO = #TEMP_TITULOS.DATAMOVTO  
		AND RECEBE_BAIXAS_EFETIVAS_ADDA121_RR3.NUMIDENTCDDA = #TEMP_TITULOS.NUMIDENTCDDA)  

	DELETE #TEMP_EFETIVAS_1 
	FROM #TEMP_EFETIVAS #TEMP_EFETIVAS_1   
	WHERE #TEMP_EFETIVAS_1.SEQ_RECEBE NOT IN (SELECT MAX(SEQ_RECEBE) 
							FROM #TEMP_EFETIVAS #TEMP_EFETIVAS_2
							WHERE #TEMP_EFETIVAS_1.NUMIDENTCDDA = #TEMP_EFETIVAS_2.NUMIDENTCDDA)  

	SELECT @SQLERRO = @@ERROR     
	IF @SQLERRO <> 0 GOTO TRATAERRO  

	-- APLICANDO RESULTADOS  
	BEGIN TRANSACTION  

		-- INCLUSOES DE TITULOS NOVOS  
		INSERT INTO TITULOS(  
			NUMIDENTCDDA, NUMCODBARRAS, VLRTITULO, SEUNUMERO,   
			CODESPECIEDOC, DATAEMISSAO, TIPOPAGTO, INDTITULONEGOCIADO,    
			VLRABATIMENTO  
		)    
		SELECT   
			#TEMP_INCLUSAO.NUMIDENTCDDA, '', 0, '',  
			'', GETDATE(), '', '',  
			0     
		FROM #TEMP_INCLUSAO   
		GROUP BY #TEMP_INCLUSAO.NUMIDENTCDDA   

		SELECT @SQLERRO = @@ERROR     
		IF @SQLERRO <> 0 GOTO TRATAERROTRANS  

		-- ATUALIZACAO DE TODOS OS TITULOS (NOVOS E EXISTENTES)  
		UPDATE TITULOS SET  
			CODIFCEDENTE = #TEMP_TITULOS.CODIFCEDENTE,    
			TIPOPESCEDENTE = #TEMP_TITULOS.TIPOPESCEDENTE,   
			CNPJ_CPF_CEDENTE = #TEMP_TITULOS.CNPJ_CPF_CEDENTE,        
			NOMECEDENTE = #TEMP_TITULOS.NOMECEDENTE,   
			NOMEFANTASIACEDENTE = #TEMP_TITULOS.NOMEFANTASIACEDENTE,  
			TIPOPESCEDENTEORI = #TEMP_TITULOS.TIPOPESCEDENTEORI,    
			CNPJ_CPF_CEDENTEORI = #TEMP_TITULOS.CNPJ_CPF_CEDENTEORI,  
			NOMECEDENTEORI = #TEMP_TITULOS.NOMECEDENTEORI,       
			ENDCEDENTEORI = #TEMP_TITULOS.ENDCEDENTEORI,   
			CIDCEDENTEORI = #TEMP_TITULOS.CIDCEDENTEORI,      
			UFCEDENTEORI = #TEMP_TITULOS.UFCEDENTEORI,   
			CEPCEDENTEORI = #TEMP_TITULOS.CEPCEDENTEORI,    
			TIPOPESSACADO = #TEMP_TITULOS.TIPOPESSACADO,   
			CNPJ_CPF_SACADO = #TEMP_TITULOS.CNPJ_CPF_SACADO,     
			NOMESACADO = #TEMP_TITULOS.NOMESACADO,   
			NOMEFANTASIASACADO = #TEMP_TITULOS.NOMEFANTASIASACADO,  
			ENDSACADO = #TEMP_TITULOS.ENDSACADO,   
			CIDSACADO = #TEMP_TITULOS.CIDSACADO,    
			UFSACADO = #TEMP_TITULOS.UFSACADO,   
			CEPSACADO = #TEMP_TITULOS.CEPSACADO,  
			TIPOPESSACADOR = #TEMP_TITULOS.TIPOPESSACADOR,    
			CNPJ_CPF_SACADOR = #TEMP_TITULOS.CNPJ_CPF_SACADOR,   
			NOMESACADOR = #TEMP_TITULOS.NOMESACADOR,   
			CODCARTEIRA = #TEMP_TITULOS.CODCARTEIRA,       
			CODMOEDACNAB = #TEMP_TITULOS.CODMOEDACNAB,   
			IDENTNOSSONUMERO = #TEMP_TITULOS.IDENTNOSSONUMERO,    
			NUMCODBARRAS = #TEMP_TITULOS.NUMCODBARRAS,   
			DATAVENCIMENTO = #TEMP_TITULOS.DATAVENCIMENTO,   
			VLRTITULO = #TEMP_TITULOS.VLRTITULO,      
			SEUNUMERO = #TEMP_TITULOS.SEUNUMERO,   
			CODESPECIEDOC = #TEMP_TITULOS.CODESPECIEDOC,   
			DATAEMISSAO = #TEMP_TITULOS.DATAEMISSAO,    
			QTDEDIASPROTESTO = #TEMP_TITULOS.QTDEDIASPROTESTO,  
			DATALIMPAGTO = #TEMP_TITULOS.DATALIMPAGTO,   
			TIPOPAGTO = #TEMP_TITULOS.TIPOPAGTO,    
			INDTITULONEGOCIADO = #TEMP_TITULOS.INDTITULONEGOCIADO,   
			VLRABATIMENTO = #TEMP_TITULOS.VLRABATIMENTO,   
			CODMORA = #TEMP_TITULOS.CODMORA,    
			DATAMORA = #TEMP_TITULOS.DATAMORA,   
			VLRPERCMORA = #TEMP_TITULOS.VLRPERCMORA,  
			CODMULTA = #TEMP_TITULOS.CODMULTA,   
			DATAMULTA = #TEMP_TITULOS.DATAMULTA,    
			VLRPERCMULTA = #TEMP_TITULOS.VLRPERCMULTA,   
			CODDESCONTO01 = #TEMP_TITULOS.CODDESCONTO01,   
			DATADESCONTO01 = #TEMP_TITULOS.DATADESCONTO01,    
			VLRPERCDESCONTO01 = #TEMP_TITULOS.VLRPERCDESCONTO01,   
			CODDESCONTO02 = #TEMP_TITULOS.CODDESCONTO02,   
			DATADESCONTO02 = #TEMP_TITULOS.DATADESCONTO02,       
			VLRPERCDESCONTO02 = #TEMP_TITULOS.VLRPERCDESCONTO02,   
			CODDESCONTO03 = #TEMP_TITULOS.CODDESCONTO03,    
			DATADESCONTO03 = #TEMP_TITULOS.DATADESCONTO03,   
			VLRPERCDESCONTO03 = #TEMP_TITULOS.VLRPERCDESCONTO03,   
			CODSITUACAO = SUBSTRING(#TEMP_TITULOS.CODSITUACAO, 2, 1),   
			DTHRSITTITULO = #TEMP_TITULOS.DTHRSIT,    
			TIPOCALCULO = #TEMP_TITULOS.TIPOCALCULO,   
			ULTNUMREFCADTIT = #TEMP_TITULOS.NUMREFCADTITULO,  
			ULTNUMSEQCADTIT = #TEMP_TITULOS.NUMSEQCADTITULO,  
			ISPBCEDENTE = #TEMP_TITULOS.ISPBCEDENTE,  
			LINHADIGITAVEL = #TEMP_TITULOS.LINHADIGITAVEL,  
			NUMPARCELA = #TEMP_TITULOS.NUMPARCELA,  
			QTDPARCELA = #TEMP_TITULOS.QTDPARCELA,  
			INDBLOQUEIO = #TEMP_TITULOS.INDBLOQPAGTO,  
			INDPARCIAL = #TEMP_TITULOS.INDPAGTOPARCIAL,  
			QTDPAGTO = #TEMP_TITULOS.QTDPAGTOPARCIAL,  
			TIPOAUTRECDIVERGENTE = #TEMP_TITULOS.TIPOAUTVLRDIVERGENTE,  
			INDVALORPERC_MIN = #TEMP_TITULOS.TIPOVLRPERCMIN,  
			VLRMINTITULO = #TEMP_TITULOS.VLRMINTITULO,   
			INDVALORPERC_MAX = #TEMP_TITULOS.TIPOVLRPERCMAX,  
			VLRMAXTITULO = #TEMP_TITULOS.VLRMAXTITULO,   
			ULTNUMREFACEITE = #TEMP_TITULOS.NUMREFCADACEITE,  
			ULTNUMSEQACEITE = #TEMP_TITULOS.NUMSEQCADACEITE,  
			ACEITE = #TEMP_TITULOS.ACEITE,       
			QTDPAGTOREG = #TEMP_TITULOS.QTDPAGTOPARCIALREGISTRADO,  
			VLRSALDOATUAL = #TEMP_TITULOS.VLRSALDOTITULO,  
			SITPAGAMENTOCIP = #TEMP_TITULOS.SITPAGTO,  
			DATAHORADDA = #TEMP_TITULOS.DATAHORADDA     
		FROM #TEMP_TITULOS, TITULOS  
		WHERE #TEMP_TITULOS.NUMIDENTCDDA = TITULOS.NUMIDENTCDDA  

		SELECT @SQLERRO = @@ERROR     
		IF @SQLERRO <> 0 GOTO TRATAERROTRANS  

		-- NOTAS FISCAIS ATUAIS  
		DELETE NOTAS_FISCAIS 
		FROM NOTAS_FISCAIS   
		INNER JOIN #TEMP_TITULOS  
			ON (NOTAS_FISCAIS.NUMIDENTCDDA = #TEMP_TITULOS.NUMIDENTCDDA)  

		SELECT @SQLERRO = @@ERROR     
		IF @SQLERRO <> 0 GOTO TRATAERROTRANS  

		INSERT INTO NOTAS_FISCAIS(
			NUMIDENTCDDA, NUMNOTAFISCAL, DATAEMISSAO, VLRNOTAFISCAL
		)  
		SELECT 
			#TEMP_NOTAS.NUMIDENTCDDA, #TEMP_NOTAS.NUMNOTAFISCAL, #TEMP_NOTAS.DATAEMISSAO, #TEMP_NOTAS.VLRNOTAFISCAL  
		FROM #TEMP_NOTAS   

		SELECT @SQLERRO = @@ERROR     
		IF @SQLERRO <> 0 GOTO TRATAERROTRANS  

		-- MENSAGENS ATUAIS  
		DELETE MENSAGENS 
		FROM MENSAGENS  
		INNER JOIN #TEMP_TITULOS  
			ON (MENSAGENS.NUMIDENTCDDA = #TEMP_TITULOS.NUMIDENTCDDA)  

		SELECT @SQLERRO = @@ERROR     
		IF @SQLERRO <> 0 GOTO TRATAERROTRANS  

		INSERT INTO MENSAGENS(
			NUMIDENTCDDA, SEQMENSAGEM, TEXTO)  
		SELECT 
			#TEMP_MENSAGENS.NUMIDENTCDDA, #TEMP_MENSAGENS.SEQMENSAGEM, #TEMP_MENSAGENS.TEXTO   
		FROM #TEMP_MENSAGENS   

		SELECT @SQLERRO = @@ERROR     
		IF @SQLERRO <> 0 GOTO TRATAERROTRANS  

		-- CALCULOS ATUAIS  
		DELETE CALCULOS 
		FROM CALCULOS  
		INNER JOIN #TEMP_TITULOS  
			ON (#TEMP_TITULOS.NUMIDENTCDDA = CALCULOS.NUMIDENTCDDA)  

		SELECT @SQLERRO = @@ERROR     
		IF @SQLERRO <> 0 GOTO TRATAERROTRANS  

		INSERT INTO CALCULOS(
			NUMIDENTCDDA, DATACALCULO, VLRMORA, VLRMULTA, VLRDESCONTO, VLRCOBRAR
		)  
		SELECT 
			NUMIDENTCDDA, DATAVALCALC, VLRJUROS, VLRMULTA, VLRDESC, VLRCOBRAR 
		FROM #TEMP_CALCULOS  


		SELECT @SQLERRO = @@ERROR     
		IF @SQLERRO <> 0 GOTO TRATAERROTRANS  

		-- TERCEIROS ATUAIS  
		DELETE TERCEIROS 
		FROM TERCEIROS  
		INNER JOIN #TEMP_TITULOS  
			ON (TERCEIROS.NUMIDENTCDDA = #TEMP_TITULOS.NUMIDENTCDDA)  

		SELECT @SQLERRO = @@ERROR     
		IF @SQLERRO <> 0 GOTO TRATAERROTRANS  

		INSERT INTO TERCEIROS(
			NUMIDENTCDDA, NUMIDENTCTERC, NUMCTRLDDA, NUMREFTERC, 
			DATAMOVTO, DATAHORADDA, TIPOPESTERC, CNPJ_CPF_TERC,
			--21/06/2022 - RFAQUINI - INCLUSÃO DOS CAMPOS TIPOPESAUTORIZADOR E CNPJ_CPF_AUTORIZADOR PARA ATUALIZAÇÃO
			TIPOPESAUTORIZADOR, CNPJ_CPF_AUTORIZADOR
		)  
		SELECT 
			#TEMP_TERCEIROS.NUMIDENTCDDA, #TEMP_TERCEIROS.NUMIDENTCTERC, #TEMP_TITULOS.NUMCTRLDDA, #TEMP_TERCEIROS.NUMREFTERC, 
			#TEMP_TERCEIROS.DATAMOVTO, #TEMP_TITULOS.DATAHORADDA, #TEMP_TERCEIROS.TIPOPESTERCEIRO, #TEMP_TERCEIROS.CNPJ_CPF_TERCEIRO,
			--21/06/2022 - RFAQUINI - INCLUSÃO DOS CAMPOS TIPOPESAUTORIZADOR E CNPJ_CPF_AUTORIZADOR PARA ATUALIZAÇÃO
			#TEMP_TERCEIROS.TIPOPESAUTORIZADOR, #TEMP_TERCEIROS.CNPJ_CPF_AUTORIZADOR
		FROM #TEMP_TERCEIROS   
		INNER JOIN #TEMP_TITULOS  
			ON (#TEMP_TITULOS.SEQ_RECEBE = #TEMP_TERCEIROS.SEQ_RECEBE)  

		SELECT @SQLERRO = @@ERROR     
		IF @SQLERRO <> 0 GOTO TRATAERROTRANS  

		UPDATE TITULOS SET 
			ULTNUMREFTERC = #TEMP_TERCEIROS.NUMREFTERC 
		FROM TITULOS  
		INNER JOIN #TEMP_TITULOS  
			ON (#TEMP_TITULOS.NUMIDENTCDDA = TITULOS.NUMIDENTCDDA)  
		INNER JOIN #TEMP_TERCEIROS  
			ON (#TEMP_TERCEIROS.NUMIDENTCDDA = TITULOS.NUMIDENTCDDA)  

		SELECT @SQLERRO = @@ERROR     
		IF @SQLERRO <> 0 GOTO TRATAERROTRANS  

		-- BAIXAS OPERACIONAIS ATUAIS  
		DELETE BAIXAS_OPERACIONAIS 
		FROM BAIXAS_OPERACIONAIS   
		INNER JOIN #TEMP_TITULOS  
			ON (BAIXAS_OPERACIONAIS.NUMIDENTCDDA = #TEMP_TITULOS.NUMIDENTCDDA)  

		SELECT @SQLERRO = @@ERROR     
		IF @SQLERRO <> 0 GOTO TRATAERROTRANS  

		INSERT INTO BAIXAS_OPERACIONAIS(  
			NUMIDENTCDDA, NUMIDENTCBAIXAOPERAC, NUMCTRLDDA, NUMREFBAIXAOPERAC,   
			NUMSEQBAIXAOPERAC, NUMREFCADTIT, DATAMOVTO, DATAHORADDA,   
			TIPOBAIXA, DATABAIXA, DATAHORABAIXA, VLRBAIXA,  
			NUMCODBARRAS, SITBAIXA, DATAHORASITUACAO, ISPBSACADA,   
			CODIFSACADA, NUMCTRLDDACANCEL, DATAHORACANCEL, SITPAGTO,   
			SITUACAOTIT, CANALPAGTO, MEIOPAGTO, INDCONTINGENCIA,   
			TIPOPESPORTADOR, CNPJ_CPF_PORTADOR, NUMSEQCADTIT,
			--20/10/2021 - RFAQUINI - INCLUSÃO E TRATAMENTO DO CAMPO NOME_PORTADOR (CATÁLOGO 5.03)
			--25/05/2022 - RFAQUINI - INCLUSÃO E TRATAMENTO DO CAMPO DATAHORARECBTTITULO (CATÁLOGO 5.04)
			NOME_PORTADOR, DATAHORARECBTTITULO,
			
			TIPOPESAGREGADOR, CNPJ_CPF_AGREGADOR, NOMEAGREGADOR /* 23/02/2023 - CAT MODERNIZAÇÃO */
		)  
		SELECT  
			#TEMP_OPERACIONAIS.NUMIDENTCDDA, #TEMP_OPERACIONAIS.NUMIDENTCBAIXAOPERAC, #TEMP_TITULOS.NUMCTRLDDA, #TEMP_OPERACIONAIS.NUMREFBAIXAOPERAC,  
			#TEMP_OPERACIONAIS.NUMSEQBAIXAOPERAC, #TEMP_TITULOS.NUMREFCADTITULO, #TEMP_TITULOS.DATAMOVTO, #TEMP_TITULOS.DATAHORADDA,  
			#TEMP_OPERACIONAIS.TIPOBAIXA, #TEMP_OPERACIONAIS.DATABAIXA, #TEMP_OPERACIONAIS.DATAHORABAIXA, #TEMP_OPERACIONAIS.VLRBAIXA,  
			#TEMP_OPERACIONAIS.NUMCODBARRAS, #TEMP_OPERACIONAIS.SITBAIXA, #TEMP_OPERACIONAIS.DATAHORASITUACAO, #TEMP_OPERACIONAIS.ISPBSACADA,  
			#TEMP_OPERACIONAIS.CODIFSACADA, #TEMP_OPERACIONAIS.NUMCTRLDDACANCEL, #TEMP_OPERACIONAIS.DATAHORACANCEL, #TEMP_TITULOS.SITPAGTO,  
			#TEMP_TITULOS.CODSITUACAO, #TEMP_OPERACIONAIS.CANALPAGTO, #TEMP_OPERACIONAIS.MEIOPAGTO, #TEMP_OPERACIONAIS.INDCONTINGENCIA,  
			#TEMP_OPERACIONAIS.TIPOPESPORTADOR, #TEMP_OPERACIONAIS.CNPJ_CPF_PORTADOR, #TEMP_TITULOS.NUMSEQCADTITULO,
			--20/10/2021 - RFAQUINI - INCLUSÃO E TRATAMENTO DO CAMPO NOME_PORTADOR (CATÁLOGO 5.03)
			--25/05/2022 - RFAQUINI - INCLUSÃO E TRATAMENTO DO CAMPO DATAHORARECBTTITULO (CATÁLOGO 5.04)
			#TEMP_OPERACIONAIS.NOME_PORTADOR, #TEMP_OPERACIONAIS.DATAHORARECBTTITULO, 
			
			#TEMP_OPERACIONAIS.TIPOPESAGREGADOR, #TEMP_OPERACIONAIS.CNPJ_CPF_AGREGADOR, #TEMP_OPERACIONAIS.NOMEAGREGADOR 	/* 23/02/2023 - CAT MODERNIZAÇÃO */
			
		FROM #TEMP_OPERACIONAIS  
		INNER JOIN #TEMP_TITULOS  
			ON (#TEMP_OPERACIONAIS.SEQ_RECEBE = #TEMP_TITULOS.SEQ_RECEBE)  

		SELECT @SQLERRO = @@ERROR     
		IF @SQLERRO <> 0 GOTO TRATAERROTRANS  

		SELECT MAX(NUMSEQBAIXAOPERAC) NUMSEQBAIXAOPERAC, NUMIDENTCDDA 
			INTO #MAX_OPERACIONAIS 
		FROM #TEMP_OPERACIONAIS  
		GROUP BY NUMIDENTCDDA  

		UPDATE TITULOS SET 
			ULTNUMREFBAIXAOPERAC = #TEMP_OPERACIONAIS.NUMREFBAIXAOPERAC, 
			ULTNUMSEQBAIXAOPERAC = #TEMP_OPERACIONAIS.NUMSEQBAIXAOPERAC   
		FROM TITULOS  
		INNER JOIN #TEMP_TITULOS  
			ON (#TEMP_TITULOS.NUMIDENTCDDA = TITULOS.NUMIDENTCDDA)  
		INNER JOIN #TEMP_OPERACIONAIS  
			ON (#TEMP_TITULOS.NUMIDENTCDDA = #TEMP_OPERACIONAIS.NUMIDENTCDDA)  
		INNER JOIN #MAX_OPERACIONAIS  
			ON (#TEMP_OPERACIONAIS.NUMSEQBAIXAOPERAC = #MAX_OPERACIONAIS.NUMSEQBAIXAOPERAC)  

		SELECT @SQLERRO = @@ERROR     
		IF @SQLERRO <> 0 GOTO TRATAERROTRANS  

		-- BAIXAS EFETIVAS ATUAIS  
		DELETE BAIXAS_EFETIVAS 
		FROM BAIXAS_EFETIVAS   
		INNER JOIN #TEMP_TITULOS  
			ON (BAIXAS_EFETIVAS.NUMIDENTCDDA = #TEMP_TITULOS.NUMIDENTCDDA)  

		SELECT @SQLERRO = @@ERROR     
		IF @SQLERRO <> 0 GOTO TRATAERROTRANS  

		INSERT INTO BAIXAS_EFETIVAS(  
			NUMIDENTCDDA, NUMIDENTCBAIXAEFET, NUMCTRLDDA, NUMREFBAIXAEFET,  
			NUMSEQBAIXAEFET, NUMREFCADTIT, DATAMOVTO, DATAHORADDA,   
			TIPOBAIXA, DATAHORABAIXA, DATABAIXA, VLRBAIXA,   
			NUMCODBARRAS, DATAHORASITUACAO, ISPBCEDENTE, CODIFCEDENTE,   
			CANALPAGTO, MEIOPAGTO, NUMIDENTCBAIXAOPERAC, NUMSEQCADTIT  
		)  
		SELECT  
			#TEMP_EFETIVAS.NUMIDENTCDDA, #TEMP_EFETIVAS.NUMIDENTCBAIXAEFET, #TEMP_TITULOS.NUMCTRLDDA, #TEMP_EFETIVAS.NUMREFBAIXAEFET,  
			#TEMP_EFETIVAS.NUMSEQBAIXAEFET, #TEMP_TITULOS.NUMREFCADTITULO, #TEMP_TITULOS.DATAMOVTO, #TEMP_TITULOS.DATAHORADDA,   
			#TEMP_EFETIVAS.TIPOBAIXA, #TEMP_EFETIVAS.DATAHORABAIXA, #TEMP_EFETIVAS.DATABAIXA, #TEMP_EFETIVAS.VLRBAIXA,   
			#TEMP_EFETIVAS.NUMCODBARRAS, #TEMP_EFETIVAS.DATAHORASITUACAO, #TEMP_EFETIVAS.ISPBCEDENTE, #TEMP_EFETIVAS.CODIFCEDENTE,   
			#TEMP_EFETIVAS.CANALPAGTO, #TEMP_EFETIVAS.MEIOPAGTO, #TEMP_EFETIVAS.NUMIDENTCBAIXAOPERAC, #TEMP_TITULOS.NUMSEQCADTITULO  
		FROM #TEMP_EFETIVAS   
		INNER JOIN #TEMP_TITULOS  
			ON (#TEMP_EFETIVAS.SEQ_RECEBE = #TEMP_TITULOS.SEQ_RECEBE)  

		SELECT @SQLERRO = @@ERROR     
		IF @SQLERRO <> 0 GOTO TRATAERROTRANS  

		SELECT MAX(NUMSEQBAIXAEFET) NUMSEQBAIXAEFET, NUMIDENTCDDA 
			INTO #MAX_EFETIVAS 
		FROM #TEMP_EFETIVAS  
		GROUP BY NUMIDENTCDDA  

		UPDATE TITULOS SET 
			ULTNUMREFBAIXAEFET = #TEMP_EFETIVAS.NUMREFBAIXAEFET, 
			ULTNUMSEQBAIXAEFET = #TEMP_EFETIVAS.NUMSEQBAIXAEFET, 
			TIPOBAIXA = #TEMP_EFETIVAS.TIPOBAIXA  
		FROM TITULOS  
		INNER JOIN #TEMP_TITULOS  
			ON (#TEMP_TITULOS.NUMIDENTCDDA = TITULOS.NUMIDENTCDDA)  
		INNER JOIN #TEMP_EFETIVAS  
			ON (#TEMP_TITULOS.NUMIDENTCDDA = #TEMP_EFETIVAS.NUMIDENTCDDA)  
		INNER JOIN #MAX_EFETIVAS  
			ON (#TEMP_EFETIVAS.NUMSEQBAIXAEFET = #MAX_EFETIVAS.NUMSEQBAIXAEFET)  

		SELECT @SQLERRO = @@ERROR     
		IF @SQLERRO <> 0 GOTO TRATAERROTRANS  

		-- RECEBES PROCESSADAS  
		UPDATE RECEBE_ADDA121_RR3 SET 
			CODPROCESSA = 1, 
			DATAMANUTENCAO = @TIMESTAMP  
		FROM RECEBE_ADDA121_RR3   
		INNER JOIN #TEMP_TITULOS  
			ON (#TEMP_TITULOS.SEQ_RECEBE = RECEBE_ADDA121_RR3.SEQ_RECEBE)  

		SELECT @SQLERRO = @@ERROR     
		IF @SQLERRO <> 0 GOTO TRATAERROTRANS  

	COMMIT TRANSACTION
	  
	RETURN  
	  
	TRATAERROTRANS:  
		ROLLBACK TRANSACTION  
		GOTO TRATAERRO  
	  
	TRATAERRO:  
		SELECT @P_SQLERRO = @SQLERRO  
		RETURN  
	  
END  
GO

INSERT INTO VERSAO_SISTEMA (
	[VERSAO], 
    [NOMESCRIPT], 
    [CODUSUARIO], 
    [DATAATU]
)
SELECT 
	'V15_01_1_01B', 
	'SP_PROCESSA_ADDA121_RR3', 
	SYSTEM_USER, 
	GETDATE() 
GO