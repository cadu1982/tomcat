USE AB_DDA
GO

IF NOT EXISTS (SELECT * FROM sys.objects WHERE name = 'SP_INCLUI_MANUT_BAIXA_OPERACIONAL')
BEGIN
	EXEC ('CREATE PROCEDURE dbo.SP_INCLUI_MANUT_BAIXA_OPERACIONAL AS BEGIN RETURN END')
END
GO

ALTER PROCEDURE dbo.SP_INCLUI_MANUT_BAIXA_OPERACIONAL
(
	@P_DATALEGADO DATETIME,
	@P_CODSISLEGADO	VARCHAR (10),
	@P_NUMCTRLLEGADO VARCHAR(35),
	@P_NUMIDENTCTIT	NUMERIC(19, 0),
	@P_NUMIDENTCBAIXAOPERAC NUMERIC(19, 0),

	@P_TIPOMANUTENCAO CHAR (1), /* I - INCLUSAO DE BAIXA | E - CANCELAMENTO DE BAIXA */
	@P_TIPOBAIXA VARCHAR (2),      /* 28/03/2023 */
	@P_DATAHORABAIXA DATETIME,
	@P_VLRBAIXA	NUMERIC(19, 5),
	@P_TIPOPESPORTADOR	CHAR(1), 

	@P_CNPJ_CPF_PORTADOR VARCHAR (14),
	--26/11/2021 - RFAQUINI, UAFERREIRA - INCLUSÃO E TRATAMENTO DO CAMPO NOME_PORTADOR (CATÁLOGO 5.03)
	@P_NOME_PORTADOR VARCHAR (80), 
	--02/06/2022 - RFAQUINI- INCLUSÃO E TRATAMENTO DO CAMPO DATAHORARECBTTITULO (CATÁLOGO 5.04)
	@P_DATAHORARECBTTITULO DATETIME,
	@P_CANALPAGTO CHAR (1),
	@P_MEIOPAGTO CHAR (1),
	@P_INDCONTINGENCIA CHAR (1),

	@P_CODEVENTO VARCHAR(4),

	--Catálogo 5.06 - Modernização da Cobrança
	@P_TIPOPESAGREGADOR CHAR(1),
	@P_CNPJ_CPF_AGREGADOR VARCHAR(14),
	@P_NOMEAGREGADOR VARCHAR(50),
	@P_AGENCIARECEBEDORA VARCHAR(4),
	@P_ISPBINICIADORPAGTO VARCHAR(8),

	@P_ERRO VARCHAR(5) OUTPUT,
	@P_MSGERRO VARCHAR(255) OUTPUT
)
AS
BEGIN

	/*
		26/11/2021 - RFAQUINI, UAFERREIRA - INCLUSÃO E TRATAMENTO DO CAMPO NOME_PORTADOR (CATÁLOGO 5.03)
							
											REMOÇÃO DE ALIAS
											
		02/06/2022 - RFAQUINI- INCLUSÃO E TRATAMENTO DO CAMPO DATAHORARECBTTITULO (CATÁLOGO 5.04)
		16-02-2023 - Leonardo Silva coloca osd domínios novos do CATALOGO DA MODERNIZAÇÃO.
		             (incluidos tipoBaixa: '4', '9', '10', '11', '12')
		26/03/2023 - Eliane - Reduzindo o domínio dos tipos de baixa, retirando tipos exclusivos dos 
		                      fluxos de acertos usados exclusivamente pelo Sistema de Cobrança COMPE
		                      e não por usuários e legados de pagamentos.
		                      (retirando tipoBaixa: '11', '12').
		                      A CIP dá erro EDDAA0749 - Só é possível informar tipo de pagamento 
		                      integral para boleto de pagamento que não permite pagamento parcial. 
		                      Acresentando '9' no domínio de canal de pagamento.
		28/03/2023 - Eliane - Ajustando tamanho do campo de tipo de baixa.
		29/03/2023 - Eliane - Acrescentando DATAREGHISTORICO.
	*/
	
	
	/* 
		SITPAGAMENTO
			NULL - INICIAL 
			1 - BLOQUEADO PRA PAGAMENTO
			2 - BAIXA OPERACIONAL ENVIADA
			3 - BAIXA CANCELADA 
	*/

    DECLARE 
		@DATAPROCESSAMENTO DATETIME, 
		@EXISTE_TITULO CHAR, 
		@TITULO_EM_ABERTO CHAR, 
		@INDBLOQUEIO CHAR, 
		@IPSBSACADA CHAR(8), 
		@CODIFSACADA CHAR(3),
		@INDPARCIAL CHAR, 
		@QTDPAGTO INT, 
		@QTDPAGTOREG INT, 
		@SITPAGAMENTO VARCHAR (10), 
		@DATAULTPROCESSAMENTO DATETIME, 
		@INDTIPOENVIOARQWS CHAR(1),
		@SISTEMABLOQUEANDO VARCHAR(10)

    SELECT  
		@P_ERRO = NULL, 
		@P_MSGERRO = NULL, 
		@DATAPROCESSAMENTO = DATAPROCESSAMENTO, 
		@DATAULTPROCESSAMENTO = DATAULTPROCESSAMENTO,
		@EXISTE_TITULO = 'N', 
		@IPSBSACADA = ISPB,
		@CODIFSACADA = CODIF,
		@INDTIPOENVIOARQWS = ISNULL(INDTIPOENVIOARQWS, 'N')
	FROM PARAMETROS

	IF(@P_TIPOBAIXA NOT IN ('0', '1', '2', '3', '4', '9', '10')) /* 16/02/2023 e 26/03/2023 */
	BEGIN
		SELECT
			@P_ERRO = '00031', 
			@P_MSGERRO = 'tipoBaixa possui um valor inválido para o domínio'
		RETURN
	END

	IF(@P_INDCONTINGENCIA NOT IN ('S', 'N'))
	BEGIN
		SELECT 
			@P_ERRO = '00031', 
			@P_MSGERRO = 'indContingencia possui um valor inválido para o domínio'
		RETURN
	END

	IF(@P_CANALPAGTO NOT IN ('0', '1', '2', '3', '4', '5', '6', '7', '8', '9' )) /* 26/03/2023 */
	BEGIN
		SELECT 
			@P_ERRO = '00031', 
			@P_MSGERRO = 'canalPagto possui um valor inválido para o domínio'
		RETURN
	END

	IF(@P_MEIOPAGTO NOT IN ('1', '2', '3', '4'))
	BEGIN
		SELECT 
			@P_ERRO = '00031', 
			@P_MSGERRO = 'meioPagto possui um valor inválido para o domínio'
		RETURN
	END

	IF(@P_TIPOPESPORTADOR NOT IN ('F', 'J'))
	BEGIN
		SELECT 
			@P_ERRO = '00031', 
			@P_MSGERRO = 'tipoPessoaPortador possui um valor inválido para o domínio'
		RETURN
	END	

	IF(@P_TIPOPESAGREGADOR  NOT IN ('F', 'J', ''))
	BEGIN
		SELECT 
			@P_ERRO = '00031', 
			@P_MSGERRO = 'tipoPessoaAgregador possui um valor inválido para o domínio'
		RETURN
	END	

	IF EXISTS(SELECT 1 FROM MANUT_TITULOS WHERE CODSISLEGADO = @P_CODSISLEGADO AND NUMCTRLLEGADO = @P_NUMCTRLLEGADO) 
	BEGIN
		SELECT 
			@P_ERRO = '00105', 
			@P_MSGERRO = 'numCtrlLegado duplicado para o sistema informado'
		RETURN
	END
    
    IF @P_NUMIDENTCTIT IS NULL 
 	BEGIN
		SELECT 
			@P_ERRO = '00101', 
			@P_MSGERRO = 'Título (' + CONVERT (VARCHAR(20),@P_NUMIDENTCTIT) + ') não cadastrado.'
		RETURN
	END
	
	SELECT 
		@EXISTE_TITULO = 'S', 
		@TITULO_EM_ABERTO = CASE 
								WHEN CODSITUACAO IS NULL 
								OR (REPLICATE(0, 2 - LEN(CODSITUACAO)) + CODSITUACAO) = '01' 
									THEN 'S' 
								ELSE 'N' 
							END,
		@INDBLOQUEIO = ISNULL(INDBLOQUEIO, 'N'), 
		@INDPARCIAL = ISNULL(INDPARCIAL, 'N'),
		@QTDPAGTO = ISNULL(QTDPAGTO, 1),  
		@QTDPAGTOREG = ISNULL (QTDPAGTOREG, 0),
		@SITPAGAMENTO = SITPAGAMENTO, 
		@SISTEMABLOQUEANDO = CODSISLEGADO
	FROM TITULOS
	WHERE NUMIDENTCDDA = @P_NUMIDENTCTIT
	
	IF 	@EXISTE_TITULO = 'N' 
	BEGIN
		SELECT 
			@P_ERRO = '00101', 
			@P_MSGERRO = 'Título (' + CONVERT (VARCHAR(20), @P_NUMIDENTCTIT) + ') não cadastrado.'
		RETURN
	END
	
	IF 	@TITULO_EM_ABERTO = 'N' 
	BEGIN
		SELECT 
			@P_ERRO = '00102', 
			@P_MSGERRO = 'Título (' + CONVERT (VARCHAR(20), @P_NUMIDENTCTIT) + ') já baixado.'
		RETURN
	END
	
	/* I - INCLUSAO DE BAIXA | E - CANCELAMENTO DE BAIXA */

	IF ISNULL(@P_TIPOMANUTENCAO, 'I') = 'I'
	BEGIN

		/* SITPAGAMENTO: NULL - INICIAL | 1 - BLOQUEADO PRA PAGAMENTO | 2 - BAIXA OPERACIONAL ENVIADA | 3 - BAIXA CANCELADA */

		IF 	@SITPAGAMENTO = '2' AND @INDPARCIAL = 'N' 
		BEGIN
			SELECT 
				@P_ERRO = '00109', 
				@P_MSGERRO = 'Baixa operacional já enviada.'
			RETURN
		END

		IF @SITPAGAMENTO IS NULL OR @SITPAGAMENTO <> '1' 
		BEGIN
			SELECT 
				@P_ERRO = '00103', 
				@P_MSGERRO = 'Título (' + CONVERT (VARCHAR(20), @P_NUMIDENTCTIT) + ') não bloqueado para pagamento.'
			RETURN
		END

		IF @SITPAGAMENTO = '1' AND @SISTEMABLOQUEANDO != @P_CODSISLEGADO
		BEGIN
			SELECT 
				@P_ERRO = '00137', 
				@P_MSGERRO = 'Título (' + CONVERT (VARCHAR(20), @P_NUMIDENTCTIT) + ') bloqueado para pagamento por outro sistema (' + @SISTEMABLOQUEANDO + ')'
			RETURN
		END

 	END
	ELSE
	BEGIN

		SELECT 1 
		FROM BAIXAS_OPERACIONAIS 
		WHERE NUMIDENTCDDA = @P_NUMIDENTCTIT
		AND NUMIDENTCBAIXAOPERAC = @P_NUMIDENTCBAIXAOPERAC
		
		IF @@ROWCOUNT = 0 
		BEGIN
			SELECT 
				@P_ERRO = '00108', 
				@P_MSGERRO = 'Baixa operacional (' +  CONVERT (VARCHAR(20), @P_NUMIDENTCBAIXAOPERAC) +  ') não cadastrada.'
			RETURN	
		END

		IF EXISTS
		(
			SELECT 1 
			FROM BAIXAS_OPERACIONAIS 
			WHERE NUMIDENTCDDA = @P_NUMIDENTCTIT 
			AND NUMIDENTCBAIXAOPERAC = @P_NUMIDENTCBAIXAOPERAC 
			AND DATAHORACANCEL IS NOT NULL
		)
		BEGIN
			SELECT 
				@P_ERRO = '00133', 
				@P_MSGERRO = 'Baixa operacional cancelada'
			RETURN
		END

		IF EXISTS(SELECT 1 
					FROM MANUT_TITULOS 
					WHERE DATAPEDIDO >= @DATAULTPROCESSAMENTO 
					AND DATAPEDIDO <= @DATAPROCESSAMENTO 
					AND TIPOMANUTENCAOTIT = 'B' 
					AND CODEVENTO = '8115' 
					AND SITUACAOPEDIDO NOT IN ('3', '5', '6') 
					AND NUMIDENTCDDA = @P_NUMIDENTCTIT 
					AND NUMIDENTCBAIXAOPERAC = @P_NUMIDENTCBAIXAOPERAC
		)
		BEGIN
			SELECT 
				@P_ERRO = '00134', 
				@P_MSGERRO = 'Já existe um pedido pendente de cancelamento para essa baixa. Aguardar retorno e processamento da mesma.'
			RETURN
		END

	END 

	INSERT INTO MANUT_TITULOS 
	(
		DATAPEDIDO, DATALEGADO, CODSISLEGADO, NUMCTRLLEGADO, 
		NUMIDENTCDDA, CODEVENTO, TIPOMANUTENCAOTIT, CODIFCEDENTE, 
		TIPOPESCEDENTE, CNPJ_CPF_CEDENTE, CODIFSACADA, NOMEFANTASIACEDENTE, 
		NOMEFANTASIACEDENTEORI, NOMEFANTASIASACADO, ISPBCEDENTE, ISPBSACADA, 
		NOMECEDENTE, TIPOPESCEDENTEORI, CNPJ_CPF_CEDENTEORI, NOMECEDENTEORI, 
		ENDCEDENTEORI, CIDCEDENTEORI, UFCEDENTEORI, CEPCEDENTEORI, 
		TIPOPESSACADO, CNPJ_CPF_SACADO, ENDSACADO, CIDSACADO, 
		UFSACADO, CEPSACADO, TIPOPESSACADOR, CNPJ_CPF_SACADOR, 
		NOMESACADOR, CODCARTEIRA, CODMOEDACNAB, IDENTNOSSONUMERO, 
		NUMCODBARRAS, DATAVENCIMENTO, VLRTITULO, SEUNUMERO, 
		CODESPECIEDOC, DATAEMISSAO, QTDEDIASPROTESTO, DATALIMPAGTO, 
		TIPOPAGTO, INDTITULONEGOCIADO, VLRABATIMENTO, CODMORA, 
		DATAMORA, VLRPERCMORA, CODMULTA, DATAMULTA, 
		VLRPERCMULTA, CODDESCONTO01, DATADESCONTO01, VLRPERCDESCONTO01, 
		CODDESCONTO02, DATADESCONTO02, VLRPERCDESCONTO02, CODDESCONTO03, 
		DATADESCONTO03, VLRPERCDESCONTO03, VLRMINTITULO, VLRMAXTITULO, 
		TIPOBAIXA, DATAPAGTOBAIXA, VLRPAGTOBAIXA, CANALPAGTO, 
		MEIOPAGTO, INDCONTINGENCIA, 
		
		NUMIDENTCBAIXAOPERAC, 
		
		CODSITUACAO, 
		SITUACAOPEDIDO, CODRETORNO, TIPOCALCULO, INDALTERAVALOR, 
		FORMAENVIO, LINHADIGITAVEL, NUMPARCELA, QTDPARCELA, 
		
		TIPOENVIO, 
		
		TIPOAUTRECDIVERGENTE, INDBLOQUEIO, INDPARCIAL, 
		QTDPAGTO, INDVALORPERC_MIN, INDVALORPERC_MAX, NOMESACADO,
		--26/11/2021 - RFAQUINI, UAFERREIRA - INCLUSÃO E TRATAMENTO DO CAMPO NOME_PORTADOR (CATÁLOGO 5.03)
		--02/06/2022 - RFAQUINI- INCLUSÃO E TRATAMENTO DO CAMPO DATAHORARECBTTITULO (CATÁLOGO 5.04)
		TIPOPESPORTADOR, CNPJ_CPF_PORTADOR, NOME_PORTADOR, DATAHORARECBTTITULO,


		--Catálogo 5.06 - Modernização da Cobrança
	        TIPOPESAGREGADOR,   
                CNPJ_CPF_AGREGADOR, 
                NOMEAGREGADOR,  
                AGENCIARECEBEDORA,  
                ISPBINICIADORPAGTO,  
		
	        DATAHORACANCELAMENTOBAIXAOPERAC, DATAREGHISTORICO /* 29/03/2023 */

	

	)
	SELECT 
		@DATAPROCESSAMENTO, @P_DATALEGADO, @P_CODSISLEGADO, @P_NUMCTRLLEGADO, 
		@P_NUMIDENTCTIT, @P_CODEVENTO, 'B', CODIFCEDENTE, 
		TIPOPESCEDENTE, CNPJ_CPF_CEDENTE, @CODIFSACADA, SUBSTRING (NOMEFANTASIACEDENTE, 1, 50), 
		SUBSTRING (NOMEFANTASIACEDENTEORI, 1, 50), SUBSTRING (NOMEFANTASIASACADO, 1, 50), ISPBCEDENTE, @IPSBSACADA, 
		NOMECEDENTE, TIPOPESCEDENTEORI, CNPJ_CPF_CEDENTEORI, NOMECEDENTEORI, 
		ENDCEDENTEORI, CIDCEDENTEORI, UFCEDENTEORI, CEPCEDENTEORI, 
		TIPOPESSACADO, CNPJ_CPF_SACADO, ENDSACADO, CIDSACADO, 
		UFSACADO, CEPSACADO, TIPOPESSACADOR, CNPJ_CPF_SACADOR, 
		NOMESACADOR, CODCARTEIRA, CODMOEDACNAB, IDENTNOSSONUMERO, 
		NUMCODBARRAS, DATAVENCIMENTO, VLRTITULO, SEUNUMERO, 
		CODESPECIEDOC, DATAEMISSAO, QTDEDIASPROTESTO, DATALIMPAGTO, 
		TIPOPAGTO, INDTITULONEGOCIADO, VLRABATIMENTO, CODMORA, 
		DATAMORA, VLRPERCMORA, CODMULTA, DATAMULTA, 
		VLRPERCMULTA, CODDESCONTO01, DATADESCONTO01, VLRPERCDESCONTO01, 
		CODDESCONTO02, DATADESCONTO02, VLRPERCDESCONTO02, CODDESCONTO03, 
		DATADESCONTO03, VLRPERCDESCONTO03, VLRMINTITULO, VLRMAXTITULO, 
		@P_TIPOBAIXA, @P_DATAHORABAIXA, @P_VLRBAIXA, @P_CANALPAGTO, 
		@P_MEIOPAGTO, ISNULL(@P_INDCONTINGENCIA, 'N'), 
		
		CASE 
			WHEN ISNULL(@P_TIPOMANUTENCAO, 'I') = 'E' 
				THEN @P_NUMIDENTCBAIXAOPERAC 
			ELSE NULL 
		END, 
		
		CODSITUACAO, 
		'0', NULL, TIPOCALCULO, INDALTERAVALOR, 
		'R', LINHADIGITAVEL, NUMPARCELA, QTDPARCELA, 
		
		CASE 
			WHEN (ISNULL(@P_INDCONTINGENCIA, 'N') = 'S' 
			OR @INDTIPOENVIOARQWS = 'S') 
				THEN 'X' 
			ELSE 'M' 
		END, 
		
		TIPOAUTRECDIVERGENTE, INDBLOQUEIO, INDPARCIAL, 
		QTDPAGTO, INDVALORPERC_MIN, INDVALORPERC_MAX, NOMESACADO,
		--26/11/2021 - RFAQUINI, UAFERREIRA - INCLUSÃO E TRATAMENTO DO CAMPO NOME_PORTADOR (CATÁLOGO 5.03)
		--02/06/2022 - RFAQUINI- INCLUSÃO E TRATAMENTO DO CAMPO DATAHORARECBTTITULO (CATÁLOGO 5.04)
		@P_TIPOPESPORTADOR, @P_CNPJ_CPF_PORTADOR, @P_NOME_PORTADOR, @P_DATAHORARECBTTITULO,

		--Catálogo 5.06 - Modernização da Cobrança
	        @P_TIPOPESAGREGADOR,
	        @P_CNPJ_CPF_AGREGADOR,
	        @P_NOMEAGREGADOR,
	        @P_AGENCIARECEBEDORA,
	        @P_ISPBINICIADORPAGTO,
		
		CASE 
			WHEN ISNULL(@P_TIPOMANUTENCAO, 'I') = 'E' 
				THEN GETDATE() 
			ELSE NULL 
		END, 
		
		GETDATE () /* 29/03/2023 */
		
	FROM TITULOS
	WHERE NUMIDENTCDDA = @P_NUMIDENTCTIT

	IF @@ROWCOUNT = 0 
		SELECT @P_ERRO = '00101', @P_MSGERRO = 'Título (' +  CONVERT (VARCHAR(20), @P_NUMIDENTCTIT) +  ') não cadastrado.'
 
END
GO

INSERT INTO VERSAO_SISTEMA (
	[VERSAO], 
	[NOMESCRIPT], 
	[CODUSUARIO], 
	[DATAATU]
)
SELECT 
	'V15_01_1_01B', 
	'SP_INCLUI_MANUT_BAIXA_OPERACIONAL', 
	SYSTEM_USER, 
	GETDATE() 
GO