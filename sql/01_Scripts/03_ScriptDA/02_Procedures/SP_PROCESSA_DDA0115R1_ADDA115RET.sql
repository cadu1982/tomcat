USE AB_DDA
GO

IF NOT EXISTS (SELECT * FROM sys.objects WHERE name = 'SP_PROCESSA_DDA0115R1_ADDA115RET')
BEGIN
	EXEC ('CREATE PROCEDURE dbo.SP_PROCESSA_DDA0115R1_ADDA115RET AS BEGIN RETURN END')
END
GO


ALTER PROCEDURE dbo.SP_PROCESSA_DDA0115R1_ADDA115RET(
	@P_TIMEOUT INT,
	@P_SQLERRO INT OUT
) AS BEGIN

/*
	MARCACOES DIFERENCIADAS (CODPROCESSA)

	"4" - DUPLICIDADE DE RECEBIMENTO (APENAS O MAIS RECENTE GERA ATUALIZACAO NA BASE)

	DOMINIOS DE REJEICOES (CODREJEICAO)

	'06' - PEDIDO CORRESPONDENTE NAO ENCONTRADO
	'01' - MANUTENCAO DE TITULO NAO CADASTRADO
	'23' - BAIXA OPERACIONAL NAO ENCONTRADA
	'24' - BAIXA OPERACIONAL JA CONSTA COMO CANCELADA
*/
/*---------------------------------------------------------------------------------------------------------------------------------------------*/
/* 09/02/2023 - Eliane - A partir da Modernização, o cancelamento de baixa somente será usado para a IF RECEBEDORA cancelar pagamentos via STR */
/*                       No caso de uma DDA0108 ter baixado efetivamente o título,o mesmo deverá ser reaberto.                                 */
/*                       MAS ESTAMOS CONSIDERANDO QUE O S TIPOS DE BAIXAS SÃO OS NOVOS DA MODERNIZAÇÃO !!! REVER ISSO !!!                      */
/* 21/03/2023 - Eliane - Na reunião de hoje com a NUCLEA perguntei se consistirão que a DDA115 ocorra somente para baixas tipo STR e a NUCLEA  */
/*                       confirmou que SIM. Dessa forma, não precisamos validar se o tipo de baixa era 9 ou 10.                                */
/*---------------------------------------------------------------------------------------------------------------------------------------------*/

	DECLARE @DATAULTPROCESSAMENTO DATETIME, @DATAPROCESSAMENTO DATETIME, @TIMESTAMP DATETIME, @SQLERRO INTEGER,
	        @VRSMANUAL VARCHAR (07) /* 09/02/2023 */
	
	
	SELECT @DATAULTPROCESSAMENTO=DATAULTPROCESSAMENTO, @DATAPROCESSAMENTO = DATAPROCESSAMENTO, @TIMESTAMP = GETDATE()
	FROM PARAMETROS WHERE CODSISTEMA = 'DDA-AUTK'

	SELECT @VRSMANUAL = VRSMANUAL FROM VWIB_VRSMANUAL WITH (NOLOCK)  /* 09/02/2023 */
        
	-- DELETA RESERVAS POR TIMEOUT
	DELETE FROM TEMP_RECEBE_ADDA115_RET_RECUSADOS WHERE DATAHORAPROC < DATEADD(MINUTE, -(@P_TIMEOUT), @TIMESTAMP)

	SELECT @SQLERRO = @@ERROR		 
	IF @SQLERRO <> 0 GOTO TRATAERRO

	DELETE FROM TEMP_RECEBE_ADDA115_RET_ACEITOS WHERE DATAHORAPROC < DATEADD(MINUTE, -(@P_TIMEOUT), @TIMESTAMP)

	SELECT @SQLERRO = @@ERROR		 
	IF @SQLERRO <> 0 GOTO TRATAERRO

	-- TRATAMENTO DE RECUSAS	
	-- BUSCA PELO QUE ESTA PENDENTE
	SELECT RECEBE.* INTO #TEMP_RECUSADOS FROM RECEBE_ADDA115_RET_RECUSADOS RECEBE
	WHERE RECEBE.CODPROCESSA = '0' AND RECEBE.DATAMOVTO >= @DATAULTPROCESSAMENTO AND RECEBE.DATAMOVTO <= @DATAPROCESSAMENTO 
	AND RECEBE.INDIMPORTACAOVALIDA = 'S'

	-- REMOVE O QUE AINDA ESTA RESERVADO
	DELETE TEMP FROM #TEMP_RECUSADOS TEMP
	INNER JOIN TEMP_RECEBE_ADDA115_RET_RECUSADOS RECEBE ON (
		TEMP.SEQ_RECEBE = RECEBE.SEQ_RECEBE 
		AND TEMP.NUMPEDIDO = RECEBE.NUMPEDIDO 
		AND TEMP.DATAMOVTO = RECEBE.DATAMOVTO
	)

	SELECT @SQLERRO = @@ERROR		 
	IF @SQLERRO <> 0 GOTO TRATAERRO

	-- RESERVA O QUE ESTIVER LIVRE
	INSERT INTO TEMP_RECEBE_ADDA115_RET_RECUSADOS(SEQ_RECEBE,NUMPEDIDO,DATAMOVTO,DATAHORAPROC)
	SELECT SEQ_RECEBE, NUMPEDIDO, DATAMOVTO, @TIMESTAMP FROM #TEMP_RECUSADOS

	-- PK DUPLICADA, PROCESSOS SIMULTANEOS?
	SELECT @SQLERRO = @@ERROR		 
	IF @SQLERRO <> 0 GOTO TRATAERRO


	IF EXISTS(SELECT 1 FROM #TEMP_RECUSADOS)
	BEGIN
		-- COD REJEICAO "06" PARA RECEBES SEM MANUTS CORRESPONDENTES
		UPDATE #TEMP_RECUSADOS SET CODREJEICAO = '06', CODPROCESSA = '2' FROM #TEMP_RECUSADOS TEMP
		WHERE NOT EXISTS (
			SELECT 1 FROM MANUT_TITULOS MANUT 
			WHERE MANUT.SITUACAOPEDIDO = '2' AND MANUT.TIPOMANUTENCAOTIT = 'B'
			AND MANUT.NUMPEDIDO = TEMP.NUMPEDIDO
			AND ISNULL(MANUT.NOMEARQRET,'') = ISNULL(TEMP.NOMEARQRET,'')
		)

		SELECT @SQLERRO = @@ERROR		 
		IF @SQLERRO <> 0 GOTO TRATAERRO

		-- NENHUMA OUTRA REJEICAO, O RESTO PODE SER DADO COMO PROCESSADO E ACEITO
		UPDATE #TEMP_RECUSADOS SET CODPROCESSA = '1' WHERE CODPROCESSA = '0' AND CODREJEICAO IS NULL

		SELECT @SQLERRO = @@ERROR		 
		IF @SQLERRO <> 0 GOTO TRATAERRO
	
		-- APLICANDO RESULTADOS
		BEGIN TRANSACTION			
			-- RECEBES PROCESSADAS
			UPDATE RECEBE_ADDA115_RET_RECUSADOS 
			SET CODPROCESSA = TEMP.CODPROCESSA, CODREJEICAO = TEMP.CODREJEICAO, DATAMANUTENCAO = @TIMESTAMP
			FROM RECEBE_ADDA115_RET_RECUSADOS RECEBE
			INNER JOIN #TEMP_RECUSADOS TEMP ON (
				TEMP.SEQ_RECEBE = RECEBE.SEQ_RECEBE 
				AND TEMP.NUMPEDIDO = RECEBE.NUMPEDIDO
				AND TEMP.DATAMOVTO = RECEBE.DATAMOVTO
			)

			SELECT @SQLERRO = @@ERROR		 
			IF @SQLERRO <> 0 GOTO TRATAERROTRANS

			-- MANUTS PROCESSADAS
			UPDATE MANUT_TITULOS
			SET SITUACAOPEDIDO = '5', CODRETORNO = '1'
			FROM MANUT_TITULOS MANUT
			INNER JOIN #TEMP_RECUSADOS TEMP ON (
				TEMP.CODPROCESSA = '1' AND MANUT.SITUACAOPEDIDO = '2'
				AND TEMP.NUMPEDIDO = MANUT.NUMPEDIDO
				AND ISNULL(TEMP.NOMEARQRET,'') = ISNULL(MANUT.NOMEARQRET, '')
			)

			SELECT @SQLERRO = @@ERROR		 
			IF @SQLERRO <> 0 GOTO TRATAERROTRANS
		
		COMMIT TRANSACTION
		
	END

	-- TRATAMENTO DE ACEITOS
	-- BUSCA O QUE ESTA PENDENTE
	SELECT RECEBE.* INTO #TEMP_ACEITOS FROM RECEBE_ADDA115_RET_ACEITOS RECEBE
	WHERE RECEBE.CODPROCESSA = '0' AND RECEBE.DATAMOVTO >= @DATAULTPROCESSAMENTO AND RECEBE.DATAMOVTO <= @DATAPROCESSAMENTO 
	AND RECEBE.INDIMPORTACAOVALIDA = 'S'

	-- REMOVE O QUE AINDA ESTA RESERVADO
	DELETE TEMP FROM #TEMP_ACEITOS TEMP
	INNER JOIN TEMP_RECEBE_ADDA115_RET_ACEITOS RECEBE ON (
		TEMP.SEQ_RECEBE = RECEBE.SEQ_RECEBE 
		AND TEMP.NUMPEDIDO = RECEBE.NUMPEDIDO
		AND TEMP.DATAMOVTO = RECEBE.DATAMOVTO
	)

	SELECT @SQLERRO = @@ERROR		 
	IF @SQLERRO <> 0 GOTO TRATAERRO

	-- RESERVA O QUE ESTA LIVRE
	INSERT INTO TEMP_RECEBE_ADDA115_RET_ACEITOS(SEQ_RECEBE,NUMPEDIDO,DATAMOVTO,DATAHORAPROC)
	SELECT TEMP.SEQ_RECEBE, TEMP.NUMPEDIDO, TEMP.DATAMOVTO, @TIMESTAMP FROM #TEMP_ACEITOS TEMP

	-- PK DUPLICADA, PROCESSOS SIMULTANEOS?
	SELECT @SQLERRO = @@ERROR		 
	IF @SQLERRO <> 0 GOTO TRATAERRO

	IF EXISTS(SELECT 1 FROM #TEMP_ACEITOS)
	BEGIN
		-- CRIANDO UMA TABELA DE TRABALHO DE MANUTS
		SELECT MANUT.* INTO #TEMP_MANUT FROM MANUT_TITULOS MANUT
		WHERE EXISTS (
			SELECT 1 FROM #TEMP_ACEITOS TEMP WHERE
			MANUT.SITUACAOPEDIDO = '2' AND TEMP.NUMPEDIDO = MANUT.NUMPEDIDO
		)

		/** REJEICOES */

		-- COD REJEICAO "06" PARA RECEBES SEM MANUT CORRESPONDENTE
		UPDATE #TEMP_ACEITOS SET CODREJEICAO = '06', CODPROCESSA = '2' FROM #TEMP_ACEITOS TEMP
		WHERE NOT EXISTS (
			SELECT 1 FROM MANUT_TITULOS MANUT 
			WHERE MANUT.SITUACAOPEDIDO = '2' AND MANUT.TIPOMANUTENCAOTIT = 'B'
			AND TEMP.NUMPEDIDO = MANUT.NUMPEDIDO
			AND ISNULL(MANUT.NOMEARQRET,'') = ISNULL(TEMP.NOMEARQRET,'')
		)

		SELECT @SQLERRO = @@ERROR		 
		IF @SQLERRO <> 0 GOTO TRATAERRO

		-- COD REJEICAO "01" PARA PEDIDOS DE UM TITULO NAO CADASTRADO
		UPDATE #TEMP_ACEITOS SET CODREJEICAO = '01', CODPROCESSA = '2' FROM #TEMP_ACEITOS TEMP
		WHERE CODPROCESSA = '0' AND CODREJEICAO IS NULL AND NOT EXISTS (
			SELECT 1 FROM TITULOS WHERE TITULOS.NUMIDENTCDDA = TEMP.NUMIDENTCDDA
		)

		SELECT @SQLERRO = @@ERROR		 
		IF @SQLERRO <> 0 GOTO TRATAERRO

		-- COD REJEICAO "23" PARA PEDIDOS DE UMA BAIXA NAO ENCONTRADA
		UPDATE #TEMP_ACEITOS SET CODREJEICAO = '23', CODPROCESSA = '2' FROM #TEMP_ACEITOS TEMP
		WHERE CODPROCESSA = '0' AND CODREJEICAO IS NULL AND NOT EXISTS (
			SELECT 1 FROM BAIXAS_OPERACIONAIS WHERE TEMP.NUMIDENTCBAIXAOPE = NUMIDENTCBAIXAOPERAC AND TEMP.NUMIDENTCDDA = NUMIDENTCDDA 
		)

		SELECT @SQLERRO = @@ERROR		 
		IF @SQLERRO <> 0 GOTO TRATAERRO

		-- COD REJEICAO "24" PARA PEDIDOS DE UM TITULO COM SITUACAO DE BAIXA CANCELADA
		UPDATE #TEMP_ACEITOS SET CODREJEICAO = '24', CODPROCESSA = '2' FROM #TEMP_ACEITOS TEMP
		WHERE CODPROCESSA = '0' AND CODREJEICAO IS NULL AND EXISTS (
			SELECT 1 FROM BAIXAS_OPERACIONAIS WHERE TEMP.NUMIDENTCBAIXAOPE = NUMIDENTCBAIXAOPERAC AND TEMP.NUMIDENTCDDA = NUMIDENTCDDA AND DATAHORACANCEL IS NOT NULL
		)

		SELECT @SQLERRO = @@ERROR		 
		IF @SQLERRO <> 0 GOTO TRATAERRO

		/** MARCACOES DIFERENCIADAS **/
		-- MOVIMENTOS SERAO SEPARADOS DOS DEMAIS PARA SOMENTE RECEBEREM A MARCACAO DE PROCESSADOS AO FINAL DA PROCEDURE

		-- CODPROCESSA "4": DUPLICIDADE DE MOVIMENTOS
		-- SE HOUVER DUPLICIDADE NO PEDIDO, MESMA BAIXA, OS RECEBIMENTOS COM DATAHORADDA OU SEQ_RECEBE MENOR SAO SEPARADAS PARA SEREM SOMENTE MARCADAS COMO PROCESSADAS AO FINAL
		SELECT MAX(TEMP.DATAHORADDA) MAX_DATAHORADDA, TEMP.NUMIDENTCBAIXAOPE INTO #TEMP_MAX_DATAHORADDA FROM #TEMP_ACEITOS TEMP
		WHERE TEMP.CODPROCESSA = '0' AND TEMP.CODREJEICAO IS NULL 
		GROUP BY TEMP.NUMIDENTCBAIXAOPE

		UPDATE #TEMP_ACEITOS SET CODPROCESSA = '4' FROM #TEMP_ACEITOS TEMP
		WHERE TEMP.CODPROCESSA = '0' AND TEMP.CODREJEICAO IS NULL 
		AND TEMP.DATAHORADDA < (SELECT MAX_DATAHORADDA FROM #TEMP_MAX_DATAHORADDA TEMP_MAX WHERE TEMP_MAX.NUMIDENTCBAIXAOPE = TEMP.NUMIDENTCBAIXAOPE)
		
		SELECT @SQLERRO = @@ERROR		 
		IF @SQLERRO <> 0 GOTO TRATAERRO

		SELECT MAX(TEMP.SEQ_RECEBE) MAX_SEQ_RECEBE, TEMP.NUMIDENTCBAIXAOPE INTO #TEMP_MAX_SEQ_RECEBE FROM #TEMP_ACEITOS TEMP
		WHERE TEMP.CODPROCESSA = '0' AND TEMP.CODREJEICAO IS NULL
		GROUP BY TEMP.NUMIDENTCBAIXAOPE
		
		UPDATE #TEMP_ACEITOS SET CODPROCESSA = '4' FROM #TEMP_ACEITOS TEMP
		WHERE TEMP.CODPROCESSA = '0' AND TEMP.CODREJEICAO IS NULL 
		AND TEMP.SEQ_RECEBE < (SELECT MAX_SEQ_RECEBE FROM #TEMP_MAX_SEQ_RECEBE TEMP_MAX WHERE TEMP_MAX.NUMIDENTCBAIXAOPE = TEMP.NUMIDENTCBAIXAOPE)

		SELECT @SQLERRO = @@ERROR		 
		IF @SQLERRO <> 0 GOTO TRATAERRO

		-- SEM MAIS REJEICOES, O RESTO PODE SER DADO COMO ACEITO E PROCESSADO
		UPDATE #TEMP_ACEITOS SET CODPROCESSA = '1' WHERE CODPROCESSA = '0' AND CODREJEICAO IS NULL

		SELECT @SQLERRO = @@ERROR		 
		IF @SQLERRO <> 0 GOTO TRATAERRO

		-- MARCANDO ANTECIPADAMENTE COMO PROCESSADO E COM O COD RETORNO CORRESPONDENTE ("0" SE NAO HOUVER REJEICAO E "1" CASO CONTRARIO)
		UPDATE #TEMP_MANUT SET SITUACAOPEDIDO = '5', CODRETORNO = '1', DATAHORADDA = TEMP.DATAHORADDA FROM #TEMP_MANUT MANUT
		INNER JOIN #TEMP_ACEITOS TEMP ON (
			TEMP.CODPROCESSA = '2' AND TEMP.CODREJEICAO IS NOT NULL AND TEMP.NUMPEDIDO = MANUT.NUMPEDIDO
		)

		SELECT @SQLERRO = @@ERROR		 
		IF @SQLERRO <> 0 GOTO TRATAERRO

		UPDATE #TEMP_MANUT SET SITUACAOPEDIDO = '5', CODRETORNO = '0', DATAHORADDA = TEMP.DATAHORADDA, NUMCTRLDDA = TEMP.NUMCTRLDDA
		FROM #TEMP_MANUT MANUT
		INNER JOIN #TEMP_ACEITOS TEMP ON (
			TEMP.CODPROCESSA = '1' AND TEMP.CODREJEICAO IS NULL AND TEMP.NUMPEDIDO = MANUT.NUMPEDIDO 
		)

		SELECT @SQLERRO = @@ERROR		 
		IF @SQLERRO <> 0 GOTO TRATAERRO
		
		-- APLICANDO RESULTADOS
		BEGIN TRANSACTION		
			
		  IF @VRSMANUAL <= '5.05'	/* 09/02/2023 - Catálogo anterior à MODERNIZAÇÃO */
		     BEGIN			/* 09/02/2023 - Catálogo anterior à MODERNIZAÇÃO */ 			
			UPDATE TITULOS 
				SET 
				SITPAGAMENTO = '3',
				CODSISLEGADO = NULL,
				DTHRSITTITULO = MT.DATAHORADDA
			FROM TITULOS
			INNER JOIN #TEMP_MANUT MT ON (MT.SITUACAOPEDIDO = '5' AND MT.CODRETORNO = '0' AND MT.NUMIDENTCDDA = TITULOS.NUMIDENTCDDA)

			SELECT @SQLERRO = @@ERROR		 
			IF @SQLERRO <> 0 GOTO TRATAERROTRANS
		     END  			/* 09/02/2023 - Catálogo anterior à MODERNIZAÇÃO */ 			
                   ELSE
/*----------------------------------------------------------------------------------------------------------------------------------------*/
/* 09/02/2023 - Dados alterados pela DDA0108 nesta data porque a DDA0115 deve ser enviada em até 2 horas após a baixa pela IF RECEBEDORA. */				
/*----------------------------------------------------------------------------------------------------------------------------------------*/
		     BEGIN			/* 09/02/2023 - Catálogo MODERNIZAÇÃO */ 			
			UPDATE TITULOS 
				SET 
				SITPAGAMENTO 		= '3',
				CODSISLEGADO 		= NULL,
				DTHRSITTITULO 		= MT.DATAHORADDA,
				
				INDCONTINGENCIA 	= NULL, 
				TIPOPESPORTADOR		= NULL, 
				CNPJ_CPF_PORTADOR 	= NULL, 
				DATAHORADDA 		= MT.DATAHORADDA,
		                CODSITUACAO 		= '1', /* EM ABERTO */
                		TIPOBAIXA      		= NULL,
                		MEIOPAGTO         	= NULL,
                		CANALPAGTO     		= NULL,
                		DATAPAGTOBAIXA 		= NULL,
                		VLRPAGTOBAIXA  		= NULL
				
			FROM TITULOS
			INNER JOIN #TEMP_MANUT MT ON 
			      (MT.SITUACAOPEDIDO = '5' AND MT.CODRETORNO = '0' AND 
			       MT.NUMIDENTCDDA   = TITULOS.NUMIDENTCDDA
			       /* 21/03/2023 - TITULOS.TIPOBAIXA IN ('9','10') BAIXAS INTEGRAL/PARCIAL VIA STR só acertos???? */ )

			SELECT @SQLERRO = @@ERROR		 
			IF @SQLERRO <> 0 GOTO TRATAERROTRANS
		     
		     END 			/* 09/02/2023 - Catálogo MODERNIZAÇÃO */ 			
/*----------------------------------------------------------------------------------------------------------------------------------------*/
/* 09/02/2023 - Dados alterados pela DDA0108 nesta data porque a DDA0115 deve ser enviada em até 2 horas após a baixa pela IF RECEBEDORA. */				
/*----------------------------------------------------------------------------------------------------------------------------------------*/

			UPDATE BAIXAS_OPERACIONAIS
				SET
				NUMCTRLDDACANCEL = MT.NUMCTRLDDA,
				DATAHORADDA = MT.DATAHORADDA,
				DATAHORACANCEL = MT.DATAHORACANCELAMENTOBAIXAOPERAC,
				ORIGEMCANCELAMENTO = '1', -- A PROPRIA IF ESTA CANCELANDO A BAIXA
				SITBAIXA = 'C' /* CANCELADA PELO PARTICIPANTE */
			FROM BAIXAS_OPERACIONAIS
			INNER JOIN #TEMP_MANUT MT ON (MT.SITUACAOPEDIDO = '5' AND MT.CODRETORNO = '0' AND MT.NUMIDENTCBAIXAOPERAC = BAIXAS_OPERACIONAIS.NUMIDENTCBAIXAOPERAC)

			SELECT @SQLERRO = @@ERROR		 
			IF @SQLERRO <> 0 GOTO TRATAERROTRANS

			-- RESTABELECENDO OS RECEBIMENTOS QUE FORAM MARCADOS PARA PROCESSAMENTO AO FINAL DA PROCEDURE
			UPDATE #TEMP_ACEITOS SET CODPROCESSA = '1' WHERE CODPROCESSA = '4'

			SELECT @SQLERRO = @@ERROR		 
			IF @SQLERRO <> 0 GOTO TRATAERROTRANS

			UPDATE #TEMP_MANUT SET SITUACAOPEDIDO = '5', CODRETORNO = '0', DATAHORADDA = TEMP.DATAHORADDA, NUMCTRLDDA = TEMP.NUMCTRLDDA
			FROM #TEMP_MANUT MANUT
			INNER JOIN #TEMP_ACEITOS TEMP ON (
				TEMP.CODPROCESSA = '1' AND TEMP.CODREJEICAO IS NULL AND TEMP.NUMPEDIDO = MANUT.NUMPEDIDO 
			)

			SELECT @SQLERRO = @@ERROR		 
			IF @SQLERRO <> 0 GOTO TRATAERRO

			-- RECEBES PROCESSADAS
			UPDATE RECEBE_ADDA115_RET_ACEITOS SET CODPROCESSA = TEMP.CODPROCESSA, CODREJEICAO = TEMP.CODREJEICAO, DATAMANUTENCAO = @TIMESTAMP
			FROM RECEBE_ADDA115_RET_ACEITOS RECEBE
			INNER JOIN #TEMP_ACEITOS TEMP ON (
				TEMP.SEQ_RECEBE = RECEBE.SEQ_RECEBE
			)

			SELECT @SQLERRO = @@ERROR		 
			IF @SQLERRO <> 0 GOTO TRATAERROTRANS

			-- MANUTS PROCESSADAS
			UPDATE MANUT_TITULOS SET SITUACAOPEDIDO = '5', CODRETORNO = TEMP.CODRETORNO, DATAHORADDA = TEMP.DATAHORADDA
			FROM MANUT_TITULOS MANUT
			INNER JOIN #TEMP_MANUT TEMP ON (
				MANUT.DATAPEDIDO = TEMP.DATAPEDIDO AND MANUT.DATALEGADO = TEMP.DATALEGADO
				AND MANUT.CODSISLEGADO = TEMP.CODSISLEGADO AND MANUT.NUMCTRLLEGADO = TEMP.NUMCTRLLEGADO
			)

			SELECT @SQLERRO = @@ERROR		 
			IF @SQLERRO <> 0 GOTO TRATAERROTRANS

		COMMIT TRANSACTION
	END

	RETURN
TRATAERROTRANS:
ROLLBACK TRANSACTION
GOTO TRATAERRO

TRATAERRO:
SELECT @P_SQLERRO = @SQLERRO
RETURN

END


GO


INSERT INTO VERSAO_SISTEMA (
	[VERSAO], 
    [NOMESCRIPT], 
    [CODUSUARIO], 
    [DATAATU]
)
SELECT 
	'V15_01_1_01B', 
	'SP_PROCESSA_DDA0115R1_ADDA115RET', 
	SYSTEM_USER, 
	GETDATE() 
GO